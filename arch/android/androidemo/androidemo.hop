;*=====================================================================*/
;*    .../project/hop/2.2.x/arch/android/androidemo/androidemo.hop     */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Tue Mar  2 07:33:54 2010                          */
;*    Last change :  Wed Oct 20 18:25:07 2010 (serrano)                */
;*    Copyright   :  2010 Manuel Serrano                               */
;*    -------------------------------------------------------------    */
;*    The Android Demonstration                                        */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module androidemo
   (library phone hopandroid)
   (import (osm-api_server "osm-api-1.0.*.hz"))
   ~(import (osm-api_client "osm-api-1.0.*.hz"))
   (import androidemo_config))

;*---------------------------------------------------------------------*/
;*    Local configuration                                              */
;*---------------------------------------------------------------------*/
(define _dir (dirname (the-loading-file)))
(define (androidemo-dir)
   _dir)

;*---------------------------------------------------------------------*/
;*    Phone descriptor ...                                             */
;*---------------------------------------------------------------------*/
(define android (instantiate::androidphone))
(define amusic (instantiate::androidmusic (phone android)))

(define sos-vibrations
   (let ((dot #l200)
	 (dash #l500)
	 (short-gap #l200)
	 (medium-gap #l500)
	 (long-gap #l1000))
      `#(#l0
	 ;; S
	 ,dot ,short-gap ,dot ,short-gap ,dot
	 ,medium-gap
	 ;; O
	 ,dash ,short-gap ,dash ,short-gap ,dash
	 ,medium-gap
	 ;; S
	 ,dot ,short-gap ,dot ,short-gap ,dot
	 ,long-gap)))

;*---------------------------------------------------------------------*/
;*    server listener                                                  */
;*---------------------------------------------------------------------*/
(add-event-listener! android "sms-delivered"
   (lambda (e)
      (hop-event-broadcast! "sms-delivered" #t))
   #t)
(add-event-listener! android "sms-sent"
   (lambda (e)
      (hop-event-broadcast! "sms-sent" #t))
   #t)
(add-event-listener! android "sms-received"
   (lambda (e)
      (hop-event-broadcast! "sms-received" e))
   #t)

;*---------------------------------------------------------------------*/
;*    androidemo ...                                                   */
;*---------------------------------------------------------------------*/
(define-service (androidemo)
   (<HTML>
      (<HEAD> :title "Hop controlled Android Phone"
	 :favicon (service-resource androidemo "etc/favicon.png")
	 :base (service-base-url androidemo (current-request))
	 :path (androidemo-dir)
	 :include "hop-audio" "androidemo" "osm-api-1.0.*.hz")
      (<ANDROIDEMO:BODY> :header "Androidemo"
	 (<DIV> :class "note"
	    "To better understand the purpose of these
demonstrations it is adviced to run them from a PC, although they can be
executed by the Web browsers running on the Android device.")
	 (<H2> "Telephony")
	 
	 (<DIV> :id "sms-send"
	    ~(add-event-listener! "sms-sent" "server"
		(lambda (e)
		   (innerHTML-set! "sms-status" "sent")))
	    ~(add-event-listener! "sms-delivered" "server"
		(lambda (e)
		   (innerHTML-set! "sms-status" "delivered")))
	    ~(add-event-listener! "sms-received" "server"
		(lambda (e)
		   (innerHTML-set! "sms-no" (car e.value))
		   (innerHTML-set! "sms-text" (cadr e.value))))
	    
	    (<DIV>
	       (<BUTTON> :onclick ~(with-hop ($(service (no text)
						  (phone-sms-send android no text))
					       (let ((no (dom-get-element-by-id "smsno")))
						  no.value)
					       (let ((txt (dom-get-element-by-id "smstext")))
						  txt.value)))
		  "Send SMS")
	       (<INPUT> :id "smsno"))
	    (<TEXTAREA> :id "smstext" "")
	    (<DIV> :id "sms-status" ""))

	 (<DIV> :id "sms-received")
	 (<DI> :id "sms-recevied-no")
	 (<PRE> :id "sms-received-text" "This box will display the content of incoming SMS")
	 
	 (<H2> "Hardware")
	 (<BUTTON> :onclick ~(with-hop ($(service ()
					    (phone-vibrate android sos-vibrations -1))))
	    "Vibrate (SOS)")
	 ~(define active-sensors #f)
	 (<DIV> "Sensor list:")
	 (<PRE> (map (lambda (l)
			(with-output-to-string
			   (lambda ()
			      (write l)
			      (newline))))
		     (phone-sensor-list android)))
	 (<BUTTON> :onclick ~(if active-sensors
				 (begin
				    (innerHTML-set! this "Activate sensors")
				    (set! active-sensors #f))
				 (let ((osm (dom-get-element-by-id "osm"))
				       (2PI (* 4 (atan 1))))
				    (innerHTML-set! this "Deactive sensors")
				    (set! active-sensors #t)
				    (let loop ()
				       (with-hop ($(service ()
						      (let ((mag (phone-sensor android 'magnetic-field))
							    (ori (phone-sensor android 'orientation)))
							 (list (cons 'orientation
								     ori)
							       (cons 'magnetic
								     mag)
							       (cons 'accelerometer
								     (phone-sensor android 'accelerometer))
							       (cons 'temperature
								     (phone-sensor android 'temperature))
							       (cons 'light
								     (phone-sensor android 'light))
							       (cons 'pressure
								     (phone-sensor android 'pressure))
							       (cons 'proximity
								     (phone-sensor android 'proximity))))))
					  (lambda (l)
					     (let ((ori (cdar l)))
						(when (pair? ori)
						   (let ((r (format "rotate(-~adeg)" (car ori))))
						      (innerHTML-set! "ori" r)
						      (node-style-set! "osm" :MozTransform r))))
					     (innerHTML-set!
					      "sensors"
					      (<TABLE> (map (lambda (r)
							       (<TR>
								  (<TD> (symbol->string (car r)))
								  (<TD> (if (pair? (cdr r))
									    (<TT> (map (lambda (v)
											  (list v " " ))
										       (cdr r)))
									    ""))))
							    l)))
					     (when active-sensors
						(after 200 loop)))))))
	    "Activate sensors")
	 (<DIV> :id "sensors")
	 (<DIV> :id "ori")
	 (<OSM:MAP> :id "osm"
	    :style "width: 300px; height: 300px; border: 1px solid red"
	    :latitude 43.619779 :longitude 7.049575)
	 ~(navigator.geolocation.getCurrentPosition
	   (lambda (p)
	      (add-event-listener! window "ready"
		 (lambda (e)
		    (let ((osm (dom-get-element-by-id "osm")))
		       (osm-set-center osm p.latitude p.longitude))))))
	 (<H2> "Multimedia")
	 (<AUDIO>
	    :controls #t
	    :src "http://www.m-base.org/motherland_pulse_mp3_files/motherland_pulse.mp3"
	    :browser 'none
	    :server (instantiate::audio-server
		       (music amusic)))
	 (<DIV> :style "font-size: 10px; font-style: italic"
	    "Thanks to " (<A> :href "http://www.m-base.com" "Steve Coleman")
	    " for making his music freely available on-line."))))

;*---------------------------------------------------------------------*/
;*    androidemo/proxy ...                                             */
;*---------------------------------------------------------------------*/
(define-service (androidemo/proxy src)
   (multiple-value-bind (scheme uinfo host port abspath)
      (http-url-parse src)
      (instantiate::http-response-remote
	 (host host)
	 (port port)
	 (path abspath)
	 (header `((Host: . ,host)))
	 (request (current-request)))))

;*---------------------------------------------------------------------*/
;*    <ANDROIDEMO:BODY> ...                                            */
;*---------------------------------------------------------------------*/
(define-xml-compound <ANDROIDEMO:BODY> ((class "androidemo")
					(header string "")
					body)
   (<BODY> :class class
      (<DIV> :align "center"
	 (<TABLE> :class "main"
	    (<TR>
	       (<TD> :class "buttons"
		  (<IMG> :class "logo" :src "etc/logo.png")
		  (<DIV> :class "version"
		     (<DIV> "Androidemo v " (androidemo-version))
		     (<DIV> (androidemo-date)))
		  (<DIV> :class "buttons"
		     (<DIV> :class "button doc"
			(<A> :title "Documentation"
			   :class "doc"
			   :href "documentation"
			   :onclick ~(set! this.href ($androidemo/documentation))
			   :target "_blank"
			   :title "androidemo documentation"
			   (<IMG> :class "doc"
			      :inline #t
			      :src (service-resource androidemo "etc/doc.png"))
			   "Help"))))
	       (<TD>
		  (<TABLE> :class "body"
		     (<TR> (<TD> :class "weblet-title"
			      (format "Androidemo - Hop Controlled Android Phone")))
		     (<TR> (<TD> :class "body" body)))))))
      (<FOOT>)))

;*---------------------------------------------------------------------*/
;*    androidemo/documentation ...                                     */
;*---------------------------------------------------------------------*/
(define-service (androidemo/documentation)
   (let* ((file (service-resource androidemo/documentation "etc/androidemo.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))
