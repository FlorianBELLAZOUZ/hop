;*=====================================================================*/
;*    .../hop/2.2.x/arch/android/androidvoice/androidvoice.hop         */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Tue Mar  2 07:33:54 2010                          */
;*    Last change :  Fri Oct 22 10:01:03 2010 (serrano)                */
;*    Copyright   :  2010 Manuel Serrano                               */
;*    -------------------------------------------------------------    */
;*    The Android Demonstration                                        */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module androidvoice
   (library phone hopandroid)
   (import androidvoice_config))

;*---------------------------------------------------------------------*/
;*    Phone descriptor ...                                             */
;*---------------------------------------------------------------------*/
(define android (instantiate::androidphone))

;*---------------------------------------------------------------------*/
;*    Install the voice recgonition plugin                             */
;*---------------------------------------------------------------------*/
(define voice-plugin
   (android-load-plugin
    (make-file-name (androidvoice-dir) "/etc/HopPluginVoice.dex")))

;*---------------------------------------------------------------------*/
;*    androidvoice ...                                                 */
;*---------------------------------------------------------------------*/
(define-service (androidvoice)
   (<HTML>
      (<HEAD> :title "Hop Android Voice Recognition"
	 :favicon (service-resource androidvoice "etc/favicon.png")
	 :base (service-base-url androidvoice (current-request))
	 :path (androidvoice-dir)
	 :include "androidvoice")
      (<ANDROIDVOICE:BODY> :header "Androidvoice"
	 (<DIV> :class "note"
	    "To better understand the purpose of these
demonstrations it is adviced to run them from a PC, although they can be
executed by the Web browsers running on the Android device.")
	 (<BUTTON> :onclick ~(with-hop ($(service ()
					    (android-send-command/result
					     (voice-plugin) #\s)))
				(lambda (h)
				   (innerHTML-set! "voicetext" h))))
		   "Start voice recognition"
	 (<DIV> :id "voicetext"))))

;*---------------------------------------------------------------------*/
;*    <ANDROIDVOICE:BODY> ...                                          */
;*---------------------------------------------------------------------*/
(define-xml-compound <ANDROIDVOICE:BODY> ((class "androidvoice")
					(header string "")
					body)
   (<BODY> :class class
      (<DIV> :align "center"
	 (<TABLE> :class "main"
	    (<TR>
	       (<TD> :class "buttons"
		  (<IMG> :class "logo" :src "etc/logo.png")
		  (<DIV> :class "version"
		     (<DIV> "Androidvoice v " (androidvoice-version))
		     (<DIV> (androidvoice-date)))
		  (<DIV> :class "buttons"
		     (<DIV> :class "button doc"
			(<A> :title "Documentation"
			   :class "doc"
			   :href "documentation"
			   :onclick ~(set! this.href ($androidvoice/documentation))
			   :target "_blank"
			   :title "androidvoice documentation"
			   (<IMG> :class "doc"
			      :inline #t
			      :src (service-resource androidvoice "etc/doc.png"))
			   "Help"))))
	       (<TD>
		  (<TABLE> :class "body"
		     (<TR> (<TD> :class "weblet-title"
			      (format "Androidvoice - Hop Controlled Android Phone")))
		     (<TR> (<TD> :class "body" body)))))))
      (<FOOT>)))

;*---------------------------------------------------------------------*/
;*    androidvoice/documentation ...                                   */
;*---------------------------------------------------------------------*/
(define-service (androidvoice/documentation)
   (let* ((file (service-resource androidvoice/documentation "etc/androidvoice.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))
