;*=====================================================================*/
;*    serrano/prgm/project/hop/2.1.x/weblets/home/home.hop             */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Wed May 10 16:18:48 2006                          */
;*    Last change :  Tue Mar  9 08:56:25 2010 (serrano)                */
;*    Copyright   :  2006-10 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP home page weblet.                                            */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module hophome
   (import  hophome_config
	    hophome_widgets
	    hophome_demo
	    hophome_wiki)
   (export  home))

;*---------------------------------------------------------------------*/
;*    logs ...                                                         */
;*---------------------------------------------------------------------*/
(define *log*
   (make-file-name (home-rc-directory) "LOG"))

(define *statistics*
   (if (file-exists? *log*)
       (let ((m (open-mmap *log* :write #f)))
	  (let loop ((i (-elong (mmap-length m) #e1))
		     (c 0))
	     (cond
		((=elong i #e-1)
		 (close-mmap m)
		 c)
		((char=? (mmap-ref m i) #\Newline)
		 (loop (-elong i #e1) (+fx c 1)))
		(else
		 (loop (-elong i #e1) c)))))
       0))

(define *log-port*
   (begin
      (unless (directory? (home-rc-directory))
	 (make-directory (home-rc-directory)))
      (or (append-output-file *log*) (open-output-file *log*))))

;*---------------------------------------------------------------------*/
;*    home-statistics ...                                              */
;*---------------------------------------------------------------------*/
(define (home-statistics)
   *statistics*)

;*---------------------------------------------------------------------*/
;*    *statistics-mutex* ...                                           */
;*---------------------------------------------------------------------*/
(define *statistics-mutex* (make-mutex "home"))

;*---------------------------------------------------------------------*/
;*    home-add-log! ...                                                */
;*---------------------------------------------------------------------*/
(define (home-add-log! req)
   (mutex-lock! *statistics-mutex*)
   (set! *statistics* (+fx 1 *statistics*))
   (display (current-date) *log-port*)
   (display " " *log-port*)
   (display " " *log-port*)
   (display (http-request-socket req) *log-port*)
   (newline *log-port*)
   (flush-output-port *log-port*)
   (mutex-unlock! *statistics-mutex*))

;*---------------------------------------------------------------------*/
;*    home ...                                                         */
;*---------------------------------------------------------------------*/
(define-service (home)
   (let ((req (current-request)))
      (home-add-log! req)
      (home-main req)))

;*---------------------------------------------------------------------*/
;*    home/rss ...                                                     */
;*---------------------------------------------------------------------*/
(define-service (home/rss)
   (let ((url "/hop/weblets/rss"))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))
   
;*---------------------------------------------------------------------*/
;*    home/devel-version-set ...                                       */
;*---------------------------------------------------------------------*/
(define-service (home/devel-version-set #!key ver)
   (if (authorized-service? (current-request) 'admin)
       (begin
	  (home-hop-devel-version-set! ver)
	  "done")
       (user-access-denied (current-request))))

;*---------------------------------------------------------------------*/
;*    home-main ...                                                    */
;*---------------------------------------------------------------------*/
(define (home-main req)
   (<HOME>
      (service-base-url home req)
      (<NOTEPAD>
	 :id "home-notepad"
	 (<NPTAB> :id "Presentation"
		  (<NPTABHEAD>
		     (<IMG> :inline #f
			:src (service-resource home "etc/gohome.png"))
		     "Main")
		  (<DELAY>
		     (lambda ()
			(download-versions-set!)
			(<DIV> :class "hop-tab"
			   (wiki->hop "presentation.wiki")))))
	 (<NPTAB> :id "videos"
		  (<NPTABHEAD>
		     (<IMG> :inline #f
			:src (service-resource home "etc/video.png"))
		     "Videos")
		  (<DELAY>
		     (lambda ()
			(<DIV> :class "hop-tab"
			   (wiki->hop "videos.wiki")))))
	 (<NPTAB> :id "demo-tab"
		  (<NPTABHEAD>
		     (<IMG> :inline #f
			:src (service-resource home "etc/demos.png"))
		     "Demos")
		  (<DELAY>
		     (lambda ()
			(<DIV> :class "hop-tab"
			   (<DEMO> req)))))
	 (<NPTAB> :id "download"
		  (<NPTABHEAD>
		     (<IMG> :inline #f
			:src (service-resource home "etc/download.png"))
		     "Download")
		  (<DELAY>
		     (lambda ()
			(download-versions-set!)
			(<DIV> :class "hop-tab"
			   (wiki->hop-no-cache "download.wiki")))))
	 (<NPTAB> :id "weblets"
		  (<NPTABHEAD>
		     (<IMG> :inline #f
			:src (service-resource home "etc/package.png"))
		     "Weblets")
		  (<DELAY>
		     (lambda ()
			(<DIV> :class "hop-tab"
			   (wiki->hop "weblets.wiki")))))
	 (<NPTAB> :id "documentation"
		  (<NPTABHEAD>
		     (<IMG> :inline #f
			:src (service-resource home "etc/doc.png"))
		     "Docs")
		  (<DELAY>
		     (lambda ()
			(<DIV> :class "hop-tab"
			   (wiki->hop "docs.wiki")))))
	 (<NPTAB> :id "faq"
		  (<NPTABHEAD>
		     (<IMG> :inline #f
			:src (service-resource home "etc/faq.png"))
		     "Faq&nbsp;&nbsp;&nbsp;")
		  (<DELAY>
		     (lambda ()
			(<DIV> :class "hop-tab"
			   (wiki->hop "faq.wiki")))))
	 (<NPTAB> :id "community"
		  (<NPTABHEAD>
		     (<IMG> :inline #f
			:src (service-resource home "etc/people.png"))
		     "Community")
		  (<DELAY>
		     (lambda ()
			(<DIV> :class "hop-tab"
			   (wiki->hop "community.wiki")))))
	 (<NPTAB> :id "stats"
		  (<NPTABHEAD>
		     (<IMG> :inline #f
			:src (service-resource home "etc/statistics.png"))
		     "Stats")
		  (<DELAY>
		     (lambda ()
			(<DIV> :class "hop-tab"
			   [The HOP (v,(hop-version)) server running this home page is up since
				,(<SPAN> :class "uptime" (hop-uptime)).
				,(<P>)
				The HOP home page has been served
				,(<SPAN> :class "uptime" (home-statistics))
				times.
				,(let ((f (make-file-path (home-dir) "motd")))
				    (if (file-exists? f)
					(<DIV> :class "motd"
					   (with-input-from-file f read-string))
					""))])))))))

;*---------------------------------------------------------------------*/
;*    home/maemo/hop.install ...                                       */
;*---------------------------------------------------------------------*/
(define-service (home/maemo/hop.install distrib catalogues)
   (instantiate::http-response-string
      (content-type "application/x-install-instructions")
      (body (format "[install]
catalogues = ~a
package  = hop

[~a]
name = Hop package for Maemo
uri = ~a/maemo
dist = ~a
components = free" catalogues catalogues (home-ftp-hop-url) distrib))))

;*---------------------------------------------------------------------*/
;*    home/documentation ...                                           */
;*---------------------------------------------------------------------*/
(define-service (home/documentation)
   (let* ((file (service-resource home/documentation "etc/home.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    home/install ...                                                 */
;*---------------------------------------------------------------------*/
(define-service (home/install)
   (<HTML>
      (<HEAD> :base (service-base-url home/install (current-request))
	 :css "home.hss")
      (<BODY> :style "background: white; font-size: small"
	      (wiki->hop "install.wiki"))))

;*---------------------------------------------------------------------*/
;*    home/install-jvm ...                                             */
;*---------------------------------------------------------------------*/
(define-service (home/install-jvm)
   (<HTML>
      (<HEAD> :base (service-base-url home/install (current-request))
	 :css "home.hss")
      (<BODY> :style "background: white; font-size: small"
	      (wiki->hop "install-jvm.wiki"))))

;*---------------------------------------------------------------------*/
;*    home/submit-hz ...                                               */
;*---------------------------------------------------------------------*/
(define-service (home/submit-hz file)
   (let ((path (cgi-post-arg-field :file file)))
      (home-main (current-request))))

;*---------------------------------------------------------------------*/
;*    downloads ...                                                    */
;*---------------------------------------------------------------------*/
(define *devel-tar-gz* #unspecified)
(define *devel-jar* #unspecified)
(define *devel-tar-url* #unspecified)
(define *devel-jar-url* #unspecified)
(define *stable-tar-gz* #unspecified)
(define *stable-dmg* #unspecified)
(define *stable-jar* #unspecified)
(define *stable-tar-url* #unspecified)
(define *stable-dmg-url* #unspecified)
(define *stable-jar-url* #unspecified)
(define *maemo-chinook-url* (home/maemo/hop.install "chinook" "internet"))
(define *maemo-freemantle-url* (home/maemo/hop.install "freemantle" "hop"))
   
;*---------------------------------------------------------------------*/
;*    download-versions-set! ...                                       */
;*---------------------------------------------------------------------*/
(define (download-versions-set!)
   (let ((devel-version (home-hop-devel-version)))

      (set! *devel-tar-gz*
	    (if (string? devel-version)
		(string-append "hop-" devel-version ".tar.gz")
		"not available"))
      
      (set! *devel-jar*
	    (if (string? devel-version)
		(string-append "java/hop-" devel-version ".jar")
		"not available"))
      
      (set! *devel-tar-url*
	    (if (string? *devel-tar-gz*)
		(make-file-name (home-ftp-hop-url) *devel-tar-gz*)
		"not available"))
      
      (set! *devel-jar-url*
	    (if (string? *devel-jar*)
		(make-file-name (home-ftp-hop-url) *devel-jar*)
		"not available"))
      
      (set! *stable-tar-gz*
	    (if (string? (home-hop-stable-version))
		(string-append "hop-" (home-hop-stable-version) ".tar.gz")
		"not available"))
      
      (set! *stable-dmg*
	    (if (string? (home-hop-stable-version))
		(string-append "macosx/hop-" (home-hop-stable-version) ".dmg")
		"not available"))
      
      (set! *stable-jar*
	    (if (string? (home-hop-stable-version))
		(string-append "java/hop-" (home-hop-stable-version) ".jar")
		"not available"))
      
      (set! *stable-tar-url*
	    (if (string? *stable-tar-gz*)
		(make-file-name (home-ftp-hop-url) *stable-tar-gz*)
		"not available"))

      (set! *stable-dmg-url*
	    (if (string? *stable-dmg*)
		(make-file-name (home-ftp-hop-url) *stable-dmg*)
		"not available"))
	    
      (set! *stable-jar-url*
	    (if (string? *stable-jar*)
		(make-file-name (home-ftp-hop-url) *stable-jar*)
		"not available"))))


