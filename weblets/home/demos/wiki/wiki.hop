(module hophome_demos-wiki
   (export (home/demos/wiki)))

(define wikiedit-event
   (symbol->string (gensym)))

(define-service (home/demos/wiki/update src)
   (let ((o (with-handler
	       (lambda (e)
		  (<SPAN> :class "error" (html-string-encode src)))
	       (wiki-string->hop src))))
      (with-output-to-string
	 (lambda ()
	    (xml-write o (current-output-port) (hop-xml-backend))))))

(define-service (home/demos/wiki)
   (let ((d (<DIV> :class "view" "")))
      (<HTML>
	 (<HEAD>
	    :title "Home Wiki demo"
	    :css (service-resource home/demos/wiki "wiki.hss"))
	 (<BODY>
	    (<H1> "Live editor")
	    (<TEXTAREA>
	       :class "playground"
	       :rows 10 :cols 60
	       :onkeyup ~(with-hop ($home/demos/wiki/update this.value)
			    (lambda (e)
			       (innerHTML-set! $d e)))
	       "Please, ++type in++ //characters// according the **Hop** wiki syntax.")
	    (<H1> "Live view")
	    d))))
	       
