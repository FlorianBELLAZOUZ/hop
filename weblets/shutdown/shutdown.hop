;*=====================================================================*/
;*    serrano/prgm/project/hop/1.9.x/weblets/shutdown/shutdown.hop     */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon Feb 14 06:14:00 2005                          */
;*    Last change :  Fri Apr  4 10:52:31 2008 (serrano)                */
;*    Copyright   :  2005-08 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP server trace facility                                        */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module weblet_shutdown
   (export shutdown
	   shutdown/kill
	   shutdown/documentation
	   shutdown/preferences))

;*---------------------------------------------------------------------*/
;*    shutdown ...                                                     */
;*---------------------------------------------------------------------*/
(define-service (shutdown)
   (let ((req (current-request)))
      (if (authorized-service? req 'admin)
	  (<SHUTDOWN> req (<SPAN> "Shutdown the serving HOP broker..."))
	  (user-access-denied req))))

;*---------------------------------------------------------------------*/
;*    shutdown/kill ...                                                */
;*---------------------------------------------------------------------*/
(define-service (shutdown/kill key)
   (when (and (equal? key (hop-process-key)) (hop-accept-kill))
      (hop-verb 1 "shutdown/kill...\n")
      (exit 0)))
       
;*---------------------------------------------------------------------*/
;*    <SHUTDOWN> ...                                                   */
;*---------------------------------------------------------------------*/
(define (<SHUTDOWN> req msg)
   (<HTML>
      (<HEAD>
	 :title "Hop Shutdown weblet"
	 :base (service-base-url shutdown req)
	 :css "shutdown.hss")
      (<BODY>
	 (<DIV> :align "center"
	    (<TABLE> :class "shutdown"
	       (<TR>
		  (<TD> (<IMG> :src "etc/logo.png"))
		  (<TD>
		     (<TABLE>
			(<TR>
			   (<TD>
			      (<DIV> :id "title"
				 "Hop Shutdown")))
			(<TR>
			   (<TD> msg))
			(<TR>
			   (<TD> :id "buttons" :align 'right
			      (<BUTTON> :onclick
				 ~(set! document.location "/hop")
				 "Cancel")
			      (<BUTTON> :id "shutdown" :onclick
				 ~(let ((but this))
				     (innerHTML-set!
				      $msg
				      (<SPAN> :class "warning"
					 "Shuting down in..."))
				     (let loop ((i 3))
					(if (>= i 1)
					    (let ((m (format "~as" i)))
					       (innerHTML-set! but m)
					       (after 1000
						      (lambda () (loop (- i 1)))))
					    (let ((body (dom-get-element-by-id "buttons")))
					       (innerHTML-set! $msg "System down...")
					       (innerHTML-set! but "0s")
					       (with-hop ($(service () (exit 0))))))))
				 "Shutdown"))))))))
	 (<FOOT>))))

;*---------------------------------------------------------------------*/
;*    shutdown-preferences ...                                         */
;*---------------------------------------------------------------------*/
(define-preferences shutdown-preferences
   ("printer" expr shutdown-print))
 
;*---------------------------------------------------------------------*/
;*    shutdown/documentation ...                                       */
;*---------------------------------------------------------------------*/
(define-service (shutdown/documentation)
   (let* ((file (service-resource shutdown/documentation "etc/shutdown.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    shutdown/preferences ...                                         */
;*---------------------------------------------------------------------*/
(define-service (shutdown/preferences)
   (shutdown-preferences-edit
    :onclick ~(with-hop ($(service ()
			     (shutdown-preferences-save shutdown-prefs))))))
