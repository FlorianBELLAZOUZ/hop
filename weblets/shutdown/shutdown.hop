;*=====================================================================*/
;*    serrano/prgm/project/hop/weblets/shutdown/shutdown.hop           */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon Feb 14 06:14:00 2005                          */
;*    Last change :  Fri Nov 16 07:13:41 2007 (serrano)                */
;*    Copyright   :  2005-07 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP server trace facility                                        */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module weblet_shutdown
   (export shutdown
	   shutdown/documentation
	   shutdown/preferences))

;*---------------------------------------------------------------------*/
;*    shutdown ...                                                     */
;*---------------------------------------------------------------------*/
(define-service (shutdown)
   (let* ((req (current-request))
	  (src-color "etc/logo.png")
	  (src-bw "etc/logo-bw.png")
	  (img (<IMG> :src "etc/logo.png"))
	  (msg (<SPAN> "Shutdown the serving HOP broker...")))
      (if (authorized-service? req 'admin)
	  (<HTML>
	     (<HEAD>
		:title "Hop Shutdown weblet"
		:base (service-base-url shutdown req)
		:css "shutdown.hss")
	     (<BODY>
		(<DIV> :align "center"
		   (<TABLE> :class "shutdown"
		      (<TR>
			 (<TD> img)
			 (<TD>
			    (<TABLE>
			       (<TR>
				  (<TD>
				     (<DIV> :id "title"
					"Hop Shutdown")))
			       (<TR>
				  (<TD> msg))
			       (<TR>
				  (<TD> :id "buttons" :align 'right
				     (<BUTTON> :onclick
					~(set! document.location "/hop")
					"Cancel")
				     (<BUTTON> :id "shutdown" :onclick
					~(let ((but this))
					    (innerHTML-set!
					     $msg
					     (<SPAN> :class "warning"
						"Shuting down in..."))
					    (set! $img.src $src-bw)
					    (let loop ((i 3))
					       (if (>= i 1)
						   (let ((m (format "~as" i)))
						      (innerHTML-set! but m)
						      (after 1000
							     (lambda () (loop (- i 1)))))
						   (let ((body (dom-get-element-by-id "buttons")))
						      (innerHTML-set! $msg "System down...")
						      (innerHTML-set! but "0s")
						      (with-hop ($(service () (exit 0))))))))
					"Shutdown"))))))))
		(<FOOT>)))
	  (user-access-denied req))))

;*---------------------------------------------------------------------*/
;*    shutdown-preferences ...                                         */
;*---------------------------------------------------------------------*/
(define-preferences shutdown-preferences
   ("printer" expr shutdown-print))
 
;*---------------------------------------------------------------------*/
;*    shutdown/documentation ...                                       */
;*---------------------------------------------------------------------*/
(define-service (shutdown/documentation)
   (let* ((file (service-resource shutdown/documentation "etc/shutdown.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    shutdown/preferences ...                                         */
;*---------------------------------------------------------------------*/
(define-service (shutdown/preferences)
   (shutdown-preferences-edit
    :onclick ~(with-hop ($(service ()
			     (shutdown-preferences-save shutdown-prefs))))))
