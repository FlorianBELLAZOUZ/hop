;*=====================================================================*/
;*    .../prgm/project/hop/2.2.x/weblets/epassword/epassword.hop       */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon Feb 14 06:14:00 2005                          */
;*    Last change :  Thu Dec 16 07:31:36 2010 (serrano)                */
;*    Copyright   :  2005-10 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP epassword encryption                                         */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module weblet_epassword
   (export (epassword/encrypt #!key user password)))

;*---------------------------------------------------------------------*/
;*    epassword ...                                                    */
;*---------------------------------------------------------------------*/
(define-service (epassword)
   (<HTML>
      (<HEAD>
	 :title "Hop Password Encryption"
	 :base (service-base-url epassword (current-request))
	 :include "hop-foot" "hop-password"
	 :css "epassword.hss")
      (<BODY>
	 ~(define (crypt el)
	     (let* ((na (dom-get-element-by-id document "user-name"))
		    (pd (dom-get-element-by-id document "password"))
		    (rm (dom-get-element-by-id document "realm"))
		    (str (digest-password-encrypt na.value pd.value rm.value)))
		(string-append "+" str)))
	 (<DIV>
	    :align "center"
	    (<TABLE>
	       :class "epassword"
	       (<TR>
		  (<TD>
		     (<IMG> :src "etc/logo.png"))
		  (<TD>
		     (<TABLE>
			(<TR>
			   (<TD> :colspan 2
			      (<DIV> :id "title" "Encrypt user identity")))
			(<TR>
			   (<TD> "User name: ")
			   (<TD> (<INPUT> :type 'text
				    :id "user-name"
				    :name "name"
				    :size 30)))
			(<TR>
			   (<TD> "Password: ")
			   (<TD> (<INPUT> :type 'password
				    :id "password"
				    :name "password"
				    :size 30)))
			(<TR>
			   (<TD> :colspan 2 ""))
			(<TR> :class "realm"
			   (<TD> :style "color: #555" "Realm: ")
			   (<TD> (<INPUT> :type 'text
				    :id "realm"
				    :name "realm"
				    :size 30
				    :value (hop-realm))))
			(<TR>
			   (<TD> :colspan 2 :style "color: #555; font-size: 60%"
			      "Change the realm value only if you know what you are doing"))
			(<TR>
			   (<TD>
			      :colspan 2
			      :align 'left
			      (<BUTTON>
				 :onclick ~(let ((e (dom-get-element-by-id
						     document "result")))
					      (innerHTML-set! e (crypt this)))
				 "crypt")))
			(<TR>
			   (<TD> :colspan 2
			      :align 'center
			      (<DIV> :id "result" "&#160;"))))))))
	 (<FOOT>))))

;*---------------------------------------------------------------------*/
;*    epassword/documentation ...                                      */
;*---------------------------------------------------------------------*/
(define-service (epassword/documentation)
   (let* ((file (service-resource epassword/documentation "etc/epassword.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    epassword/encrypt ...                                            */
;*---------------------------------------------------------------------*/
(define-service (epassword/encrypt #!key user password)
   (cond
      ((not (string? user))
       (error 'epassword/encrypt "illegal user name" user))
      ((not (string? password))
       (error 'epassword/encrypt "illegal password" password))
      (else
       (string-append "+" (digest-password-encrypt user password (hop-realm))))))
