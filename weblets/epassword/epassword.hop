;*=====================================================================*/
;*    serrano/prgm/project/hop/weblets/epassword/epassword.hop         */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon Feb 14 06:14:00 2005                          */
;*    Last change :  Fri Nov 23 07:16:44 2007 (serrano)                */
;*    Copyright   :  2005-07 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP epassword encryption                                         */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module weblet_epassword)

;*---------------------------------------------------------------------*/
;*    epassword ...                                                    */
;*---------------------------------------------------------------------*/
(define-service (epassword)
   (<HTML>
      (<HEAD>
	 :title "Hop Password Encryption"
	 :base (service-base-url epassword (current-request))
	 :include "md5"
	 :css "epassword.hss")
      (<BODY>
	 ~(define (crypt el)
	     (let* ((na (dom-get-element-by-id document "user-name"))
		    (pd (dom-get-element-by-id document "password"))
		    (str (string-append na.value " " pd.value)))
		(hex_md5 str)))
	 (<DIV>
	    :align "center"
	    (<TABLE>
	       :class "epassword"
	       (<TR>
		  (<TD>
		     (<IMG> :src "etc/logo.png"))
		  (<TD>
		     (<TABLE>
			(<TR>
			   (<TD> :colspan 2
			      (<DIV> :id "title" "Encrypt user identity")))
			(<TR>
			   (<TD> "User name: ")
			   (<TD> (<INPUT> :type 'text
				    :id "user-name"
				    :name "name"
				    :size 30)))
			(<TR>
			   (<TD> "Password: ")
			   (<TD> (<INPUT> :type 'password
				    :id "password"
				    :name "password"
				    :size 30)))
			(<TR>
			   (<TD>
			      :colspan 2
			      :align 'left
			      (<BUTTON>
				 :onclick ~(let ((e (dom-get-element-by-id
						     document "result")))
					      (set! e.innerHTML (crypt this)))
				 "crypt")))
			(<TR>
			   (<TD> :colspan 2
			      :align 'center
			      (<DIV> :id "result" "&#160;"))))))))
	 (<FOOT>))))

;*---------------------------------------------------------------------*/
;*    epassword/documentation ...                                      */
;*---------------------------------------------------------------------*/
(define-service (epassword/documentation)
   (let* ((file (service-resource epassword/documentation "etc/epassword.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    epassword/encrypt ...                                            */
;*---------------------------------------------------------------------*/
(define-service (epassword/encrypt user password)
   (cond
      ((not (string? user))
       (error 'epassword/encrypt "illegal user name" user))
      ((not (string? password))
       (error 'epassword/encrypt "illegal password" password))
      (else
       (md5sum (string-append user " " password)))))
