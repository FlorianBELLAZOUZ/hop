;*=====================================================================*/
;*    serrano/prgm/project/hop/2.1.x/weblets/trace/trace.hop           */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon Feb 14 06:14:00 2005                          */
;*    Last change :  Thu Jul  1 07:10:45 2010 (serrano)                */
;*    Copyright   :  2005-10 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP server trace facility                                        */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module weblet_trace
   (export trace
	   trace/tprint
	   (trace-print)
	   (trace-print-set!)
	   trace/documentation
	   trace/preferences))

;*---------------------------------------------------------------------*/
;*    Global parameters                                                */
;*---------------------------------------------------------------------*/
(define-parameter trace-print
   (lambda (req args)
      (hop-verb 1 (hop-color req req " TRACE: "))
      (when (pair? args)
	 (for-each (lambda (x) (hop-verb 1 x)) args))
      (hop-verb 1 "\n")))

(define-preferences trace-preferences
   ("printer" expr trace-print))

;*---------------------------------------------------------------------*/
;*    trace-mutex ...                                                  */
;*---------------------------------------------------------------------*/
(define trace-mutex (make-mutex))

;*---------------------------------------------------------------------*/
;*    trace ...                                                        */
;*---------------------------------------------------------------------*/
(define-service (trace . args)
   (let ((req (current-request))
	 (file (service-resource trace "etc/trace.wiki")))
      (mutex-lock! trace-mutex)
      ((trace-print) req args)
      (mutex-unlock! trace-mutex)
      (http-warning [The Trace weblet produces no document. It merely implements
 services that print messages on the server console.,(<BR>)

 See the ,(<A> :href (format "/hop/doc/weblet?weblet=~a" file)"documentation")
 for the TRACE weblet.])))

;*---------------------------------------------------------------------*/
;*    trace/tprint ...                                                 */
;*---------------------------------------------------------------------*/
(define-service (trace/tprint file pos args)
   (mutex-lock! trace-mutex)
   (apply fprint (current-error-port) file "," pos ":" args)
   (mutex-unlock! trace-mutex))

;*---------------------------------------------------------------------*/
;*    trace/documentation ...                                          */
;*---------------------------------------------------------------------*/
(define-service (trace/documentation)
   (let* ((file (service-resource trace/documentation "etc/trace.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    trace/preferences ...                                            */
;*---------------------------------------------------------------------*/
(define-service (trace/preferences)
   (trace-preferences-edit
    :onclick ~(with-hop ($(service ()
			     (trace-preferences-save trace-prefs))))))

;*---------------------------------------------------------------------*/
;*    The user configuration                                           */
;*---------------------------------------------------------------------*/
(hop-load-rc "tracerc.hop")

