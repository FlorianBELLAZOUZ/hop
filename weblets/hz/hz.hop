;*=====================================================================*/
;*    serrano/prgm/project/hop/3.0.x/weblets/hz/hz.hop                 */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Fri Mar 17 08:37:11 2006                          */
;*    Last change :  Wed Nov 19 17:59:41 2014 (serrano)                */
;*    Copyright   :  2006-20 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP weblets managers.                                            */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module hz
   
   (import hz_config
	   hz_xml
	   hz_db
	   hz_weblet
	   hz_install
	   hz_package
	   hz_api)
   
   (export hz
	   hz/documentation
	   hz/preferences
	   hz/install
	   hz/uninstall
	   hz/remove
	   hz/exists
	   hz/run))

;*---------------------------------------------------------------------*/
;*    hz ...                                                           */
;*---------------------------------------------------------------------*/
(define-service (hz)
   (let ((req (current-request)))
      (if (authorized-service? req 'admin)
	  (begin
	     (when (file-exists? (hz-prefs)) (hz-preferences-load (hz-prefs)))
	     (hz-db-init!)
	     (<HZ> req))
	  (access-denied req))))

;*---------------------------------------------------------------------*/
;*    hz/documentation ...                                             */
;*---------------------------------------------------------------------*/
(define-service (hz/documentation)
   (let* ((file (service-resource hz/documentation "etc/hz.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    hz/preferences ...                                               */
;*---------------------------------------------------------------------*/
(define-service (hz/preferences)
   (let ((req (current-request)))
      (if (not (authorized-service? req 'admin))
	  (access-denied req)
	  (<HTML>
	     (<HEAD>
		:title "Hop Hz Preferences"
		:base (service-base-url hz/preferences req)
		:favicon "etc/favicon.png"
		(<META> :name "viewport"
		   :content "width=device-width, initial-scale=1.")
		:include "hop-foot" "hop-prefs"
		:css "hz.hss")
	     (<HZ:BODY>
		(<DIV> :id "body"
		   (<HZ:PREFERENCES>)))))))

;*---------------------------------------------------------------------*/
;*    hz/install ...                                                   */
;*---------------------------------------------------------------------*/
(define-service (hz/install #!key url checksum)
   (let ((req (current-request)))
      (tprint "req=" req)
      (if (not (authorized-service? req 'admin))
	  (access-denied req)
	  (begin
	     (when (file-exists? (hz-prefs)) (hz-preferences-load (hz-prefs)))
	     (let ((w (hz-install-url url :checksum checksum)))
		(cond
		   ((isa? w weblet)
		    (<HZ> req :weblet w))
		   ((list? w)
		    (match-case w
		       ((?status ?url ?msg)
			(http-error
			   (format "Cannot install weblet from ~s\n~a" url msg)
			   req))
		       (else
			(http-error w req))))
		   (else
		    (http-error w req))))))))

;*---------------------------------------------------------------------*/
;*    hz/uninstall ...                                                 */
;*---------------------------------------------------------------------*/
(define-service (hz/uninstall #!key name url id)
   (let ((req (current-request)))
      (if (not (authorized-service? req 'admin))
	  (access-denied req)
	  (begin
	     (when (file-exists? (hz-prefs)) (hz-preferences-load (hz-prefs)))
	     (hz-db-init!)
	     (let ((w (cond
			 (name (hz-db-find-by-name name))
			 (url (hz-db-find-by-url url))
			 (id (hz-db-find-by-id id)))))
		(if (isa? w weblet)
		    (begin
		       (hz-uninstall-weblet w)
		       #t)
		    (http-error
		       (format "Cannot find weblet ~s" id)
		       req)))))))

;*---------------------------------------------------------------------*/
;*    hz/remove ...                                                    */
;*---------------------------------------------------------------------*/
(define-service (hz/remove #!key name url id)
   (let ((req (current-request)))
      (if (not (authorized-service? req 'admin))
	  (access-denied req)
	  (begin
	     (when (file-exists? (hz-prefs)) (hz-preferences-load (hz-prefs)))
	     (hz-db-init!)
	     (let ((w (cond
			 (name (hz-db-find-by-name name))
			 (url (hz-db-find-by-url url))
			 (id (hz-db-find-by-id id)))))
		(if (isa? w weblet)
		    (begin
		       (hz-remove-weblet w req)
		       #t)
		    (http-error
		       (format "Cannot find weblet ~s" id)
		       req)))))))

;*---------------------------------------------------------------------*/
;*    hz/exists ...                                                    */
;*    -------------------------------------------------------------    */
;*    Returns #t iff the weblet already exists.                        */
;*---------------------------------------------------------------------*/
(define-service (hz/exists #!key name)
   (service-exists? name))

;*---------------------------------------------------------------------*/
;*    hz/run ...                                                       */
;*---------------------------------------------------------------------*/
(define-service (hz/run #!key url install service #!rest args)
   
   (define (run name version)
      (<HTML>
	 (let ((svc (string-append "/hop/"
				   (if service
				       (string-append name "/" service)
				       name))))
	    ~(set! document.location $(hop-apply-nice-url svc args)))))
   
   (hz-db-init!)
   (multiple-value-bind (name version)
      (package-url-parse url)
      (with-handler
	 (lambda (e) (http-error e (current-request)))
	 (begin
	    (if install
		(let ((w (or (let ((w (hz-db-find-by-url url)))
				(when (and (isa? w weblet)
					   (with-access::weblet w (directory)
					      (directory? directory)))
				   w))
			     (hz-install-url url))))
		   (cond
		      ((isa? w weblet)
		       (run name version))
		      ((not (pair? w))
		       (http-error w (current-request)))
		      (else
		       (case (car w)
			  ((404)
			   (http-error (instantiate::&io-file-not-found-error
					  (proc "hz/run")
					  (msg "File not found")
					  (obj url))
			      (current-request)))
			  (else
			   (http-error (instantiate::&io-error
					  (proc "hz/run")
					  (msg (format "File not found (~a)"
						       (car w)))
					  (obj url))
			      (current-request)))))))
		(begin
		   (hz-load url)
		   (run name version)))))))

;*---------------------------------------------------------------------*/
;*    hz/check ...                                                     */
;*---------------------------------------------------------------------*/
(define-service (hz/check)
   (instantiate::http-response-file
      (start-line "HTTP/1.1 200 ok")
      (file (service-resource hz "etc/favicon.png"))))

;*---------------------------------------------------------------------*/
;*    The user configuration                                           */
;*---------------------------------------------------------------------*/
(hop-load-rc "hzrc.hop")

