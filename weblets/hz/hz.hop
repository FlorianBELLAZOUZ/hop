;*=====================================================================*/
;*    serrano/prgm/project/hop/weblets/hz/hz.hop                       */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Fri Mar 17 08:37:11 2006                          */
;*    Last change :  Sat Nov 18 15:59:29 2006 (serrano)                */
;*    Copyright   :  2006 Manuel Serrano                               */
;*    -------------------------------------------------------------    */
;*    HOP weblets managers.                                            */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module hz
   (import hz_config
	   hz_authenticate
	   hz_sync
	   hz_widgets
	   hz_db
	   hz_weblets
	   hz_repository
	   hz_install))

;*---------------------------------------------------------------------*/
;*    hz ...                                                           */
;*---------------------------------------------------------------------*/
(define-service (hz)
   (let ((req (current-request)))
      (multiple-value-bind (user mode)
	 (hz-get-access req)
	 (cond
	    ((and (user? user)
		  (symbol? mode)
		  (user-authorized-service? user 'hz))
	     (hz-main req user mode))
	    (else
	     (hz-authenticate req))))))

;*---------------------------------------------------------------------*/
;*    hz/documentation ...                                             */
;*---------------------------------------------------------------------*/
(define-service (hz/documentation)
   (let* ((base (dirname (hop-service-load-path hz/documentation)))
	  (file (string-append base "/etc/hz.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    hz/install-ondemand ...                                          */
;*---------------------------------------------------------------------*/
(define-service (hz/install-ondemand name url)
   (hz-install-from-url #t (current-request) name url))

;*---------------------------------------------------------------------*/
;*    logout ...                                                       */
;*---------------------------------------------------------------------*/
(define (logout req)
   (let ((path (http-request-path req)))
      ~(let ((path $path))
	  (cookie-remove! "hop_hz" path)
	  (set! location path))))

;*---------------------------------------------------------------------*/
;*    hz-ondemand ...                                                  */
;*---------------------------------------------------------------------*/
(define (hz-ondemand req user mode name url)
   (let ((db (hz-get-db (hz-db-path mode)))
	 (prefs (make-file-name (hz-rc-directory) "hz.prefs")))
      (when (file-exists? prefs) (hz-preferences-load prefs))
      (if (hz-install-ondemand db req name url)
	  (hz-main req user mode))))

;*---------------------------------------------------------------------*/
;*    hz-install-from-url ...                                          */
;*---------------------------------------------------------------------*/
(define (hz-install-from-url require-authentication req name url)
   (cond
      (require-authentication
       (multiple-value-bind (user mode)
	  (hz-get-access req)
	  (cond
	     ((and (user? user)
		   (symbol? mode)
		   (user-authorized-service? user 'hz))
	      (hz-ondemand req user mode name url))
	     (else
	      (user-access-denied req)))))
      ((http-request-localhostp req)
       (hz-ondemand req #f 'local name url))
      (else
       (user-access-denied req))))

;*---------------------------------------------------------------------*/
;*    hz-direct-install ...                                            */
;*---------------------------------------------------------------------*/
(define (hz-direct-install req name url)
   (cond
      ((string=? name "")
       (error 'hz "name is empty" name))
      ((string=? url "")
       (error 'hz "URL is empty" url))
      (else
       (let* ((prefix (format "http://~a:~a" (hostname) (hop-port)))
	      (req (current-request))
	      (url (cond
		      ((substring=? "http://" url 7)
		       url)
		      ((substring=? "file://" url 7)
		       (string-append prefix
				      (substring url 7 (string-length url))))
		      (else
		       (string-append prefix url)))))
	  (hz-install-from-url #f req name url)))))

;*---------------------------------------------------------------------*/
;*    hz-direct-install-tab ...                                        */
;*---------------------------------------------------------------------*/
(define (hz-direct-install-tab)
  (define (find-weblet-name url)
    (let* ((guess (prefix (basename url)))
	   (index (string-index guess "-")))
      (if (> index 0)
	  (substring guess 0 index)
	  guess)))
  
  (<DIV> :class "hz-ondemand-install"
    (<FORM> :action (service (url)
		       (hz-direct-install (current-request)
					  (find-weblet-name url)
					  url))
	(<B> "Weblet URL: ")
	(<INPUT> :size 40 :name "url")
	(<BR>)
        (<BUTTON> :onclick ~(submit) "Install"))))


;*---------------------------------------------------------------------*/
;*    hz-main ...                                                      */
;*---------------------------------------------------------------------*/
(define (hz-main req user mode)
   (let ((db (hz-get-db (hz-db-path mode)))
	 (prefs (make-file-name (hz-rc-directory) "hz.prefs")))
      (when (file-exists? prefs) (hz-preferences-load prefs))
      (<HTML>
	 (<HEAD>
	    :title "Hop Hz"
	    :include "hop-notepad" "hop-paned" "hop-tree" "hop-foldlist"
	    :include "hop-iwindow"
	    :base (service-base hz req)
	    :css "hz.hss")
	 (<ABODY>
	    (<DIV>
	       :id "body"
	       (<VBOX>
		  (<VBOX-BLOCK>
		     (<TABLE>
			:class "identity"
			(<TR>
			   (<TH> "User: ")
			   (<TD> (if (user? user) (user-name user) ""))
;* 			   (<TH> "Mode: ")                             */
;* 			   (<TD> :class mode)                          */
			   (<TD> (<BUTTON>
				    :onclick (logout req)
				    "logout")))))
		  (<VBOX-SEPARATOR>)
		  (<VBOX-BLOCK>
		     (<NOTEPAD>
			(<NPHEAD> "")
			(<NPTAB>
			   (<NPTABHEAD> "Sync")
			   (<SYNC> db))
			(<NPTAB>
			   (<NPTABHEAD> "Weblets")
			   (<DELAY>
			      (lambda ()
				 (<WEBLETS> db))))
			(<NPTAB>
			   (<NPTABHEAD> "Direct install")
			   (hz-direct-install-tab))
			(<NPTAB>
			   (<NPTABHEAD> "Preferences")
			   (<PREFERENCES> prefs))))))))))

;*---------------------------------------------------------------------*/
;*    hz/preferences ...                                               */
;*---------------------------------------------------------------------*/
(define-service (hz/preferences)
   (let ((req (current-request)))
      (if (not (authorized-service? req 'admin))
	  (user-access-denied req)
	  (<HTML>
	     (<HEAD>
		:title "Hop Hz"
		:include "hop-notepad" "hop-paned" "hop-tree" "hop-foldlist"
		:include "hop-iwindow"
		:base (service-base hz/preferences req)
		:css "hz.hss")
	     (<ABODY>
		(<DIV>
		   :id "body"
		   (<PREFERENCES>
		      (make-file-name (hz-rc-directory) "hz.prefs"))))))))

;*---------------------------------------------------------------------*/
;*    <PREFERENCES> ...                                                */
;*---------------------------------------------------------------------*/
(define (<PREFERENCES> prefs)
   (hz-preferences-edit
    :onclick ~(with-hop ($(service ()
			     (let ((d (hz-rc-directory)))
				(unless (directory? d)
				   (make-directory d)))
			     (hz-preferences-save prefs)
			     prefs)))))

;*---------------------------------------------------------------------*/
;*    hz/list ...                                                      */
;*    -------------------------------------------------------------    */
;*    Returns the list of available weblets. This does not needs       */
;*    authentication when used on a local server.                      */
;*---------------------------------------------------------------------*/
(define-service (hz/list)
   (let ((req (current-request)))
      (if (or (http-request-localhostp req)
	      (authorized-service? req 'hz)
	      (authorized-service? req 'hz/list))
	  (hz-list req)
	  (multiple-value-bind (user mode)
	     (hz-get-access req)
	     (if (and (user? user)
		      (symbol? mode)
		      (user-authorized-service? user 'hz))
		 (hz-list req user mode)
		 (user-access-denied req))))))

;*---------------------------------------------------------------------*/
;*    hz-list ...                                                      */
;*---------------------------------------------------------------------*/
(define (hz-list req)
   (let ((db (hz-get-repository-db)))
      (<HTML>
	 (<HEAD>
	    :title "Hop Hz"
	    :include "hop-notepad" "hop-paned" "hop-tree" "hop-foldlist"
	    :include "hop-iwindow")
	 (<BODY>
	    :class "hz-list"
	    (<TREE>
	       :id "main-tree"
	       :open #t
	       (<TRHEAD>
		  (let ((dt (hz-db-sync-date db)))
		     (format "~a ~a ~a"
			     (date-day dt)
			     (month-aname (date-month dt))
			     (date-year dt))))
	       (<TRBODY>
		  (map (lambda (c)
			  (<TREE>
			     (<TRHEAD> :value c c)
			     (<TRBODY>
				(<DELAY>
				   (lambda ()
				      (let* ((icon (cond
						      ((assoc c (hz-category-icons)) => cdr)
						      (else "file.png")))
					     (ifile (make-file-path (hz-dir)
								    "etc"
								    "icons"
								    icon)))
					 (map (lambda (e)
						 (<TRLEAF>
						    :file ifile
						    (<SPAN>
						       :class "weblet"
						       (dbhz-name e)
						       " -- "
						       (<SPAN> :class "sdescr" (dbhz-sdescr e)))))
					      (hz-db-get-entries-by-category db c))))))))
		       (hz-db-get-categories db))))))))
