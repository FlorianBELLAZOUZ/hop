;*=====================================================================*/
;*    serrano/prgm/project/hop/3.0.x/weblets/hz/install.hop            */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Fri Mar 24 09:21:45 2006                          */
;*    Last change :  Tue Jun 24 11:01:11 2014 (serrano)                */
;*    Copyright   :  2006-20 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    Actual installation/uninstallation.                              */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module hz_install
   
   (import hz_config
	   hz_package
	   hz_weblet
	   hz_db)
   
   (export (hz-load ::bstring)
	   (hz-install-weblet ::weblet #!key override checksum)
	   (hz-download-url ::bstring #!key auth weblet override checksum)
	   (hz-install-url ::bstring #!key auth weblet override publisher checksum)
	   (hz-uninstall-weblet ::weblet)
	   (hz-remove-weblet ::weblet)))

;*---------------------------------------------------------------------*/
;*    hz-check-weblet ...                                              */
;*    -------------------------------------------------------------    */
;*    Check that one weblet can be installed (check depends). Returns  */
;*    #t on success, returns the name of the failing weblet and        */
;*    the reason of the failure.                                       */
;*---------------------------------------------------------------------*/
(define (hz-check-weblet e)
   (with-access::weblet e (depends)
      #t))
   
;*---------------------------------------------------------------------*/
;*    hz-load ...                                                      */
;*---------------------------------------------------------------------*/
(define (hz-load path)
   (with-url path
      (lambda (s)
	 (let* ((p0 (open-input-string s))
		(p (open-input-gzip-port p0)))
	    (unwind-protect
	       (let* ((tmp (make-file-name (os-tmp) "hop"))
		      (files (untar p :directory tmp)))
		  (if (pair? files)
		      (let* ((file (car files))
			     (base (substring file
					      (+fx (string-length tmp) 1)
					      (string-length file)))
			     (dir (dirname base))
			     (name (if (string=? dir ".") base dir))
			     (src (make-file-path
				   tmp name (string-append name ".hop"))))
			 (if (file-exists? src)
			     (hop-load-weblet src)
			     (error "hz-load" "Cannot find HOP source" path)))
		      (error "hz-load" "Cannot find HOP source" path)))
	       (begin
		  (close-input-port p)
		  (close-input-port p0)))))
      :timeout (hz-connection-timeout)))

;*---------------------------------------------------------------------*/
;*    hz-install-weblet ...                                            */
;*---------------------------------------------------------------------*/
(define (hz-install-weblet e #!key override checksum)
   (with-access::weblet e (name url authorization)
      (hz-install-url url :auth authorization :weblet e :override override
	 :checksum checksum)))

;*---------------------------------------------------------------------*/
;*    hz-download-url ...                                              */
;*---------------------------------------------------------------------*/
(define (hz-download-url url #!key auth weblet override checksum)

   (define (download url name version path sum)
      (let ((header (cond
		       ((string? auth)
			(list (list authorization: auth)))
		       ((pair? auth)
			(list (list authorization:
				 (http-basic-authorization
				    (car auth) (cdr auth)))))
		       (else
			'()))))
	 (with-url url
	    (lambda (str)
	       ;; download the file
	       (with-output-to-file path
		  (lambda ()
		     (display str)))
	       ;; generate the md5sum file
	       (with-output-to-file sum
		  (lambda ()
		     (display (md5sum str))))
	       ;; return the path name
	       path)
	    :fail (lambda (xhr)
		     (with-access::xml-http-request xhr (input-port status)
			;; raise an error to stop everything
			(if (=fx status 404)
			    (list status url "File not found")
			    (let ((msg (read-string input-port)))
			       ;; WARNING, MS 19dec2010: this code is incorrect,
			       ;; READ-STRING is not correct. A true parsing
			       ;; (with content-length) should be needed here.
			       (list status url msg)))))
	    :timeout (hz-connection-timeout)
	    :header header)))

   (define (checksum? checksum hzsum)
      (or (not checksum)
	  (with-handler
	     (lambda (e) #f)
	     (equal? checksum (call-with-input-file hzsum read-string)))))

   (multiple-value-bind (name version)
      (package-url-parse url)
      (let* ((hzfile (format "~a-~a.hz" name version))
	     (sumfile (format "~a-~a.md5sum" name version))
	     (hzpath (make-file-name (hz-download-directory) hzfile))
	     (sumpath (make-file-name (hz-download-directory) sumfile)))
	 (unless (directory? (hz-download-directory))
	    (unless (make-directories (hz-download-directory))
	       (error "hz-download-url" "Cannot create download directory"
		  (hz-download-directory))))
	 (cond
	    ((or (not (file-exists? hzpath))
		 (not (file-exists? sumpath))
		 (not (checksum? checksum sumpath)))
	     (download url name version hzpath sumpath))
	    ((not override)
	     hzpath)
	    (else
	     (delete-file hzpath)
	     (download url name version hzpath sumpath))))))
   
;*---------------------------------------------------------------------*/
;*    hz-install-url ...                                               */
;*---------------------------------------------------------------------*/
(define (hz-install-url url #!key auth weblet override publisher checksum)
   (hz-db-init!)
   (when (string? url)
      (multiple-value-bind (name version)
	 (package-url-parse url)
	 (let ((r (hz-download-url url :auth auth
		     :weblet weblet :override override :checksum checksum)))
	    (if (and (string? r) (file-exists? r))
		;; now downloaded, install
		(let ((w (hz-install-file weblet r name version url)))
		   ;; add the publisher to the publisher list
		   (when (and (string? publisher)
			      (not (string-null? publisher)))
		      (hz-publisher-add! publisher))
		   (reset-autoload!)
		   w)
		;; propagate the failure
		r)))))

;*---------------------------------------------------------------------*/
;*    hz->weblet ...                                                   */
;*---------------------------------------------------------------------*/
(define (hz->weblet name version url)
   
   (define (get-info dir def)
      (let ((l (get-weblet-info dir)))
	 (if (pair? l)
	     l
	     (def))))
   
   (let* ((dir (make-file-name (hz-weblets-directory) name))
	  (i (get-info dir
		(lambda ()
		   (get-info
		      (make-file-name dir (string-append name "-" version))
		      (lambda ()
			 `((name ,name) (version ,version))))))))
      (info->weblet i :name name :version version :url url :dir dir)))

;*---------------------------------------------------------------------*/
;*    hz-install-file ...                                              */
;*---------------------------------------------------------------------*/
(define (hz-install-file e path name version url)
   ;; untar the .hz into the weblets directory
   (let* ((p (open-input-gzip-file path)))
      (unless (directory? (hz-weblets-directory))
	 (unless (make-directories (hz-weblets-directory))
	    (error "hz-install-file" "Cannot create weblet directory"
	       (hz-weblets-directory))))
      (let ((dir (make-file-name (hz-weblets-directory) name)))
	 (make-directories dir)
	 (unwind-protect
	    (untar p :directory dir)
	    (close-input-port p))))
   ;; add the weblet to the database
   (let ((e (or e (hz->weblet name version url))))
      ;; add the publisher for other possible weblets
      (with-access::weblet e (stamp publisher install version)
	 (set! stamp (hz-db-sync-stamp))
	 ;; mark the weblet as installed
	 (unless (or (not (string? publisher)) (string=? publisher ""))
	    (hz-publisher-add! publisher))
	 ;; update the database
	 (set! install version))
      (hz-db-add-or-update! e)
      ;; try to configure the weblet
      e))
   
;*---------------------------------------------------------------------*/
;*    hz-uninstall-weblet ...                                          */
;*---------------------------------------------------------------------*/
(define (hz-uninstall-weblet w)
   (hz-db-init!)
   (when (isa? w weblet)
      (with-access::weblet w (name install)
	 (delete-path (make-file-path (hz-weblets-directory) name))
	 (set! install "")
	 (hz-db-update! w))))

;*---------------------------------------------------------------------*/
;*    hz-remove-weblet ...                                             */
;*---------------------------------------------------------------------*/
(define (hz-remove-weblet w)
   (hz-db-init!)
   (when (isa? w weblet)
      (hz-uninstall-weblet w)
      ;; remove the RC file
      (with-access::weblet w (name install)
	 (let* ((rcbase (make-file-name (hop-rc-directory)
			   (string-append name "rc")))
		(hop (string-append rcbase ".hop"))
		(js (string-append rcbase "js")))
	    (when (file-exists? hop)
	       (delete-file hop))
	    (when (file-exists? js)
	       (delete-file js))))
      ;; remove the download hz file
      (tprint "REMOVE HZ DOWNLOAD TODO...")))
