;*=====================================================================*/
;*    serrano/prgm/project/hop/2.0.x/weblets/hz/install.hop            */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Fri Mar 24 09:21:45 2006                          */
;*    Last change :  Wed May  6 10:25:34 2009 (serrano)                */
;*    Copyright   :  2006-09 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    Actual installation/uninstallation.                              */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module hz_install
   
   (import hz_config
	   hz_package
	   hz_weblet
	   hz_db)
   
   (export (hz-install-url ::bstring #!key auth)
	   (hz-install-weblet ::weblet)
	   (hz-load ::bstring)
	   (hz-uninstall-weblet ::weblet)))

;*---------------------------------------------------------------------*/
;*    hz-install-weblet ...                                            */
;*---------------------------------------------------------------------*/
(define (hz-install-weblet e)
   (hz-install-url (weblet-url e)
		   :auth (weblet-authorization e)
		   :weblet e))

;*---------------------------------------------------------------------*/
;*    hz-install-url ...                                               */
;*---------------------------------------------------------------------*/
(define (hz-install-url url #!key auth weblet)
   (hz-db-init!)
   (multiple-value-bind (name version)
      (package-url-parse url)
      (let* ((hzfile (format "~a-~a.hz" name version))
	     (hzpath (make-file-name (hz-download-directory) hzfile))
	     (header (cond
			((string? auth)
			 (list (list authorization: auth)))
			((pair? auth)
			 (list (list authorization:
				     (http-basic-authorization
				      (car auth) (cdr auth)))))
			(else
			 '()))))
	 (when (file-exists? hzpath)
	    (delete-file hzpath))
	 (with-url url
	    (lambda (str)
	       ;; download the file
	       (with-output-to-file hzpath
		  (lambda ()
		     (display str)))
	       ;; install it
	       (let ((w (hz-install-file weblet hzpath name version url)))
		  (reset-autoload!)
		  w))
	    :fail (lambda (xhr)
		     ;; raise an error to stop everything
		     (let* ((iport (xml-http-request-input-port xhr))
			    (msg (read-string iport)))
			(list (xml-http-request-status xhr) url msg)))
	    :timeout (hz-connection-timeout)
	    :header header))))

;*---------------------------------------------------------------------*/
;*    hz->weblet ...                                                   */
;*---------------------------------------------------------------------*/
(define (hz->weblet name version url)
   (let* ((p (make-file-path (hz-weblets-directory) name "etc" "weblet.info"))
	  (i (if (file-exists? p)
		 (with-input-from-file p read)
		 `((name ,name) (version ,version)))))
      (info->weblet i :name name :version version :url url)))

;*---------------------------------------------------------------------*/
;*    hz-install-file ...                                              */
;*---------------------------------------------------------------------*/
(define (hz-install-file e path name version url)
   ;; untar the .hz into the weblets directory
   (let* ((p (open-input-gzip-file path)))
      (unwind-protect
	 (untar p :directory (hz-weblets-directory))
	 (close-input-port p)))
   ;; add the weblet to the database
   (let ((e (or e (hz->weblet name version url))))
      ;; add the publisher for other possible weblets
      (weblet-stamp-set! e (hz-db-sync-stamp))
      ;; mark the weblet as installed
      (let ((pub (weblet-publisher e)))
	 (unless (or (not (string? pub)) (string=? pub ""))
	    (hz-publisher-add! pub)))
      ;; update the database
      (weblet-install-set! e (weblet-version e))
      (hz-db-add-or-update! e)
      ;; try to configure the weblet
      e))
   
;*---------------------------------------------------------------------*/
;*    hz-load ...                                                      */
;*---------------------------------------------------------------------*/
(define (hz-load path)
   (with-url path
      (lambda (s)
	 (let* ((p0 (open-input-string s))
		(p (open-input-gzip-port p0)))
	    (unwind-protect
	       (let* ((tmp (make-file-name (os-tmp) "hop"))
		      (files (untar p :directory tmp)))
		  (if (pair? files)
		      (let* ((file (car files))
			     (base (substring file
					      (+fx (string-length tmp) 1)
					      (string-length file)))
			     (dir (dirname base))
			     (name (if (string=? dir ".") base dir))
			     (src (make-file-path
				   tmp name (string-append name ".hop"))))
			 (if (file-exists? src)
			     (hop-load src)
			     (error 'hz-load "Cannot find HOP source" path)))
		      (error 'hz-load "Cannot find HOP source" path)))
	       (begin
		  (close-input-port p)
		  (close-input-port p0)))))
      :timeout (hz-connection-timeout)))

;*---------------------------------------------------------------------*/
;*    hz-uninstall-weblet ...                                          */
;*---------------------------------------------------------------------*/
(define (hz-uninstall-weblet w)
   (hz-db-init!)
   (when (weblet? w)
      (with-access::weblet w (name install)
	 (when (delete-path (make-file-path (hz-weblets-directory) name))
	    (set! install "")
	    (hz-db-add-or-update! w)))))
