;*=====================================================================*/
;*    .../prgm/project/hop/2.0.x/weblets/dashboard/dashboard.hop       */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon Feb 14 06:14:00 2005                          */
;*    Last change :  Tue Nov 24 10:24:04 2009 (serrano)                */
;*    Copyright   :  2005-09 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP dashboard                                                    */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module weblet_dashboard
   (export dashboard
	   dashboard/documentation
	   dashboard/populate))

;*---------------------------------------------------------------------*/
;*    dashboard/documentation ...                                      */
;*---------------------------------------------------------------------*/
(define-service (dashboard/documentation)
   (let* ((file (service-resource dashboard/documentation "etc/dashboard.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    dashboard ...                                                    */
;*---------------------------------------------------------------------*/
(define-service (dashboard)
   (let* ((file (service-resource dashboard/documentation "etc/dashboard.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (<HTML>
	 (<HEAD> :title "Hop dashboard"
	    :base (service-base-url dashboard (current-request))
	    :css "dashboard.hss"
	    :include "hop-dashboard"
	    :favicon (img-base64-encode
		      (service-resource dashboard "etc/favicon.png")))
	 (<BODY>
	    (<DIV> :align "center"
	       (<TABLE> :class "dashboard"
		  (<COLGROUP> (<COL> :width "0*"))
		  (<TR>
		     (<TD> :id "logo" (<IMG> :id "logo" :src "etc/logo.png"))
		     (<TD> (<TABLE> :width "100%"
			      (<TR> (<TD> :id "title" "Hop Dashboard"))
			      (<TR> (<TD> [
Dashboard is a Hop weblet that manages a small toolbar on the left-side of
the bottom of Web pages (such as the one present in this page). This toolbar
is intended to be used by ,(<EM> "other") weblets. As such ,(<TT> "dashboard")
provides a service that is not supposed to be used directly. Please refer to
the Dashboard ,(<A> :href url :target "_blank" "documentation") for
further investigation.]))
			      (<TR> (<TD> (<BR>)))
			      (<TR> (<TD> "The list of applets:"))
			      (<TR> (<TD> (<APPLETS>))))))))
	    (<FOOT>)))))

;*---------------------------------------------------------------------*/
;*    applets ...                                                      */
;*---------------------------------------------------------------------*/
(define (applets)
   (sort (lambda (s1 s2)
	    (cond
	       ((string=? (car s1) "hop") #t)
	       ((string=? (car s2) "hop") #f)
	       (else (string<=? (car s1) (car s2)))))
	 (hop-dashboard-weblet-applets)))

;*---------------------------------------------------------------------*/
;*    <APPLETS> ...                                                    */
;*---------------------------------------------------------------------*/
(define (<APPLETS>)
   (<UL>
      (map (lambda (x)
	      (let* ((svc (caddr x))
		     (url (if (or (string-prefix? "http://" svc)
				  (string-prefix? "https://" svc))
			      svc
			      (dirname svc))))
		 (<LI> (<A> :href url (<TT> url)))))
	   (applets))))

;*---------------------------------------------------------------------*/
;*    dashboard/populate ...                                           */
;*---------------------------------------------------------------------*/
(define-service (dashboard/populate)
   (applets))
