;*=====================================================================*/
;*    serrano/prgm/project/hop/weblets/dashboard/dashboard.hop         */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon Feb 14 06:14:00 2005                          */
;*    Last change :  Tue Jul 10 08:26:49 2007 (serrano)                */
;*    Copyright   :  2005-07 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP dashboard                                                    */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module weblet_dashboard
   (export dashboard
	   dashboard/documentation
	   dashboard/populate))

;*---------------------------------------------------------------------*/
;*    dashboard ...                                                    */
;*---------------------------------------------------------------------*/
(define-service (dashboard)
   (<HTML>
      (<HEAD> :title "Hop dashboard"
	 :base (service-base-url dashboard (current-request))
	 :css "dashboard.hss")
      (<BODY>
	 (<DIV> :align "center"
	    (<TABLE> :class "dashboard"
	       (<COLGROUP> (<COL> :width "0*"))
	       (<TR>
		  (<TD> :id "logo" (<IMG> :id "logo" :src "etc/logo.png"))
		  (<TD> (<TABLE> :width "100%"
			   (<TR> (<TD> :id "title" "Hop Dashboard"))
			   (<TR> (<TD> "")))))))
	 (<FOOT>))))
   
;*---------------------------------------------------------------------*/
;*    dashboard/documentation ...                                      */
;*---------------------------------------------------------------------*/
(define-service (dashboard/documentation)
   (let* ((file (service-resource dashboard/documentation "etc/dashboard.wiki"))
	  (url (format "/hop/dashboard/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    dashboard-icon ...                                               */
;*---------------------------------------------------------------------*/
(define (dashboard-icon icon)
   (service-resource dashboard (make-file-name "etc" icon)))

;*---------------------------------------------------------------------*/
;*    dashboard/rfc ...                                                */
;*---------------------------------------------------------------------*/
(define-service (dashboard/rfc)
   (let ((rfc (<INPUT> :type 'text :size 40))
	 (repo (<INPUT> :type 'text :size 40
		  :value "http://tools.ietf.org/html")))
      (<DASHBOARD> :style "width: 35em; height: 14ex"
	 (<TABLE>
	    (<TR>
	       (<TD> :rowspan 4 :valign 'top
		  (<IMG> :src (dashboard-icon "dashboard-rfc.png"))))
	    (<TR>
	       (<TH> :class "kind" "Rfc")
	       (<TD> rfc))
	    (<TR>
	       (<TH> :class "kind" "Repository")
	       (<TD> repo))
	    (<TR>
	       (<TD> :style "padding-top: 1ex" :colspan 2 :align 'right
		  (<A> :href "http://tools.ietf.org/html"
		     :class "button"
		     :target "_blank"
		     :onclick ~(set! this.href
				     (format "~a/rfc~a" $repo.value $rfc.value))
		     "Go")))))))

;*---------------------------------------------------------------------*/
;*    <DASHBOARD> ...                                                  */
;*---------------------------------------------------------------------*/
(define-xml-compound <DASHBOARD> ((style #f)
				  (class #f)
				  body)
   (<HTML>
      (<HEAD> :css (service-resource dashboard "dashboard-applet.hss"))
      (<BODY> :class (if (string? class)
			 (string-append "hop-dashboard-applet " class)
			 "hop-dashboard-applet")
	      :style style
	      body)))

;*---------------------------------------------------------------------*/
;*    dashboard/populate ...                                           */
;*---------------------------------------------------------------------*/
(define-service (dashboard/populate)
   (append (hop-dashboard-weblet-applets)
	   (list (list "rfc"
		       (dashboard-icon "dashboard-rfc.png")
		       (make-service-url dashboard/rfc)))))
