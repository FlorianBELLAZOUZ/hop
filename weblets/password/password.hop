;*=====================================================================*/
;*    serrano/prgm/project/hop/weblets/password/password.hop           */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon Feb 14 06:14:00 2005                          */
;*    Last change :  Fri Feb 10 16:34:09 2006 (eg)                     */
;*    Copyright   :  2005-06 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP password encryption                                          */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module weblet_password)

;*---------------------------------------------------------------------*/
;*    password-directory ...                                           */
;*---------------------------------------------------------------------*/
(define password-directory
   (make-file-name (hop-weblets-directory) "password"))

;*---------------------------------------------------------------------*/
;*    password filter ...                                              */
;*---------------------------------------------------------------------*/
(define-service (password)
   (<HTML>
      (<HEAD>
	 (<HOP-HEAD>)
	 (<SCRIPT> :type "text/javascript"
		   :src (format "~a/md5.js" (hop-share-directory)))
	 (<SCRIPT> :type "text/javascript"
		   { function crypt( el ) {
                        var na = document.getElementById( "user-name" )
                        var pd = document.getElementById( "password" )
                        var str = na.value + " " + pd.value
 
                        return hex_md5( str )
                   }})				      
	 (<LINK> :rel "stylesheet"
		 :type "text/css"
		 :href (format "~a/password.hss" password-directory)))
      (<BODY>
	 (<CENTER>
	    (<TABLE>
	       :class "password"
	       (<TR>
		  (<TD>
		     (<IMG> :src (format "~a/password.png"
					 password-directory)))
		  (<TD>
		     (<TABLE>
			(<TR>
			   (<TD> :colspan 2
				 (<DIV> :id "title" "Encrypt user identity")))
			(<TR>
			   (<TD> "User name: ")
			   (<TD> (<INPUT> :type 'text
					  :id "user-name"
					  :name "name"
					  :size 30)))
			(<TR>
			   (<TD> "Password: ")
			   (<TD> (<INPUT> :type 'password
					  :id "password"
					  :name "password"
					  :size 30)))
			(<TR>
			   (<TD>
			      :colspan 2
			      :align 'left
			      (<BUTTON>
				 :onclick {
				    document.getElementById( "result" ).innerHTML = crypt( this )
				    } "crypt")))
			(<TR>
			   (<TD> :colspan 2
				 :align 'center
				 (<DIV> :id "result" "&nbsp;")))))))))))

;*---------------------------------------------------------------------*/
;*    password about ...                                               */
;*---------------------------------------------------------------------*/
(define-service (password/about)
  (<WEBLET-ABOUT> :title "password"
		  :subtitle "A MD5 Password Generator"
		  :icon (format "~a/password.png" password-directory)
		  :version (hop-version)
   (<P> [This simple weblet encodes the given user name and password in MD5.])))
