;*=====================================================================*/
;*    serrano/prgm/project/hop/weblets/password/password.hop           */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon Feb 14 06:14:00 2005                          */
;*    Last change :  Mon Oct 23 15:50:47 2006 (serrano)                */
;*    Copyright   :  2005-06 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP password encryption                                          */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module weblet_password)

;*---------------------------------------------------------------------*/
;*    password-directory ...                                           */
;*---------------------------------------------------------------------*/
(define password-directory
   (make-file-name (hop-weblets-directory) "password"))

;*---------------------------------------------------------------------*/
;*    password ...                                                     */
;*---------------------------------------------------------------------*/
(define-service (password)
   (let ((req (the-current-request)))
      (if (authorized-service? req 'password)
	  (password-main)
	  (user-access-denied req))))

;*---------------------------------------------------------------------*/
;*    password/documentation ...                                       */
;*---------------------------------------------------------------------*/
(define-service (password/documentation)
   (let* ((file (string-append password-directory "/etc/password.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    password-main ...                                                */
;*---------------------------------------------------------------------*/
(define (password-main)
   (<HTML>
      (<HEAD>
	 (<SCRIPT> :type "text/javascript"
		   :src (format "~a/md5.js" (hop-share-directory)))
	 (<SCRIPT> :type "text/javascript"
		   { function crypt( el ) {
                        var na = document.getElementById( "user-name" )
                        var pd = document.getElementById( "password" )
                        var str = na.value + " " + pd.value
 
                        return hex_md5( str )
                   }})				      
	 (<LINK> :rel "stylesheet"
		 :type "text/css"
		 :href (format "~a/password.hss" password-directory)))
      (<BODY>
	 (<DIV>
	    :align "center"
	    (<TABLE>
	       :class "password"
	       (<TR>
		  (<TD>
		     (<IMG> :src
			    (format "~a/etc/logo.png" password-directory)))
		  (<TD>
		     (<TABLE>
			(<TR>
			   (<TD> :colspan 2
				 (<DIV> :id "title" "Encrypt user identity")))
			(<TR>
			   (<TD> "User name: ")
			   (<TD> (<INPUT> :type 'text
					  :id "user-name"
					  :name "name"
					  :size 30)))
			(<TR>
			   (<TD> "Password: ")
			   (<TD> (<INPUT> :type 'password
					  :id "password"
					  :name "password"
					  :size 30)))
			(<TR>
			   (<TD>
			      :colspan 2
			      :align 'left
			      (<BUTTON>
				 :onclick {
				    document.getElementById( "result" ).innerHTML = crypt( this )
				    } "crypt")))
			(<TR>
			   (<TD> :colspan 2
				 :align 'center
				 (<DIV> :id "result" "&nbsp;"))))))))
	 (<FOOT>))))

;*---------------------------------------------------------------------*/
;*    password/encrypt ...                                             */
;*---------------------------------------------------------------------*/
(define-service (password/encrypt user password)
   (cond
      ((not (string? user))
       (error 'password/encrypt "illegal user name" user))
      ((not (string? password))
       (error 'password/encrypt "illegal password" password))
      (else
       (md5sum (string-append user " " password)))))
