;*=====================================================================*/
;*    serrano/prgm/project/hop/weblets/password/password.hop           */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon Feb 14 06:14:00 2005                          */
;*    Last change :  Tue Nov 14 11:24:48 2006 (serrano)                */
;*    Copyright   :  2005-06 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP password encryption                                          */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module weblet_password)

;*---------------------------------------------------------------------*/
;*    password-directory ...                                           */
;*---------------------------------------------------------------------*/
(define password-directory
   (make-file-name (hop-weblets-directory) "password"))

;*---------------------------------------------------------------------*/
;*    password ...                                                     */
;*---------------------------------------------------------------------*/
(define-service (password)
   (<HTML>
      (<HEAD>
	 (<TITLE> "Hop Password Encryption")
	 (<SCRIPT> :type "text/javascript"
	    :src (format "~a/md5.js" (hop-share-directory)))
	 (<LINK> :rel "stylesheet"
	    :type "text/css"
	    :href (format "~a/password.hss" password-directory)))
      (<BODY>
	 ~(define (crypt el)
	     (let* ((na (dom-get-element-by-id document "user-name"))
		    (pd (dom-get-element-by-id document "password"))
		    (str (string-append na.value " " pd.value)))
		(hex_md5 str)))
	 (<DIV>
	    :align "center"
	    (<TABLE>
	       :class "password"
	       (<TR>
		  (<TD>
		     (<IMG> :src
			(format "~a/etc/logo.png" password-directory)))
		  (<TD>
		     (<TABLE>
			(<TR>
			   (<TD> :colspan 2
			      (<DIV> :id "title" "Encrypt user identity")))
			(<TR>
			   (<TD> "User name: ")
			   (<TD> (<INPUT> :type 'text
				    :id "user-name"
				    :name "name"
				    :size 30)))
			(<TR>
			   (<TD> "Password: ")
			   (<TD> (<INPUT> :type 'password
				    :id "password"
				    :name "password"
				    :size 30)))
			(<TR>
			   (<TD>
			      :colspan 2
			      :align 'left
			      (<BUTTON>
				 :onclick ~(let ((e (dom-get-element-by-id
						     document "result")))
					      (set! e.innerHTML (crypt this)))
				 "crypt")))
			(<TR>
			   (<TD> :colspan 2
			      :align 'center
			      (<DIV> :id "result" "&nbsp;"))))))))
	 (<FOOT>))))

;*---------------------------------------------------------------------*/
;*    password/documentation ...                                       */
;*---------------------------------------------------------------------*/
(define-service (password/documentation)
   (let* ((file (string-append password-directory "/etc/password.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    password/encrypt ...                                             */
;*---------------------------------------------------------------------*/
(define-service (password/encrypt user password)
   (cond
      ((not (string? user))
       (error 'password/encrypt "illegal user name" user))
      ((not (string? password))
       (error 'password/encrypt "illegal password" password))
      (else
       (md5sum (string-append user " " password)))))
