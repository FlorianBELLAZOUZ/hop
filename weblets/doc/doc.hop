;*=====================================================================*/
;*    serrano/prgm/project/hop/weblets/doc/doc.hop                     */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Sun Apr  9 14:42:21 2006                          */
;*    Last change :  Mon May 22 16:08:35 2006 (serrano)                */
;*    Copyright   :  2006 Manuel Serrano                               */
;*    -------------------------------------------------------------    */
;*    HOP meta-documentation.                                          */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module hopdoc
   (library hopwiki)
   (import  hopdoc_config
	    hopdoc_api
	    hopdoc_wiki
	    hopdoc_widgets
	    hopdoc_services
	    hopdoc_tutorials
	    hopdoc_libraries
	    hopdoc_search))

;*---------------------------------------------------------------------*/
;*    doc ...                                                          */
;*---------------------------------------------------------------------*/
(define-service (doc)
   (if (and (users-added?) (not (authorized-service? req 'doc)))
       (user-access-denied req)
       (doc-main (the-current-request))))

;*---------------------------------------------------------------------*/
;*    doc-main ...                                                     */
;*---------------------------------------------------------------------*/
(define (doc-main req)
   (let ((prefs (make-file-name (doc-rc-directory) "doc.prefs")))
      (thread-start! (make-thread (lambda () (setup-api-tables!))))
      (when (file-exists? prefs) (doc-preferences-load prefs))
      (<DOC>
	 (<NOTEPAD>
	    :id "doc-notepad"
	    (<NPHEAD> "")
	    (<NPTAB> :id "doc-api"
		     (<NPTABHEAD> "API")
		     (<API>))
	    (<NPTAB> :id "doc-core"
		     (<NPTABHEAD> "Core language")
		     (<DIV>
			:class "wiki"
			(doc-page
			 (make-file-name (doc-core-dir) "core.wiki")
			 (make-wiki-request-syntax (doc-wiki-syntax)
						   req))))
	    (<NPTAB> :id "doc-libraries"
		     (<NPTABHEAD> "Libraries")
		     (<LIBRARIES>))
	    (<NPTAB> :id "doc-wiki"
		     (<NPTABHEAD> "Wiki syntax")
		     (<DIV>
			:class "wiki"
			(doc-page
			 (make-file-name (doc-wiki-dir) "syntax.wiki")
			 (make-wiki-request-syntax (doc-wiki-syntax)
						   req))))
	    (<NPTAB> :id "doc-tutorials"
		     (<NPTABHEAD> "Tutorials")
		     (<TUTORIALS> req))))))
