;*=====================================================================*/
;*    serrano/prgm/project/hop/2.0.x/weblets/doc/doc.hop               */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Sun Apr  9 14:42:21 2006                          */
;*    Last change :  Tue Jan 12 15:37:42 2010 (serrano)                */
;*    Copyright   :  2006-10 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP meta-documentation.                                          */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module hopdoc
   (import  hopdoc_config
	    hopdoc_api
	    hopdoc_wiki
	    hopdoc_widgets
	    hopdoc_services
	    hopdoc_tutorials
	    hopdoc_libraries
	    hopdoc_weblets
	    hopdoc_index
	    hopdoc_search))

;*---------------------------------------------------------------------*/
;*    doc ...                                                          */
;*---------------------------------------------------------------------*/
(define-service (doc)
   (doc-main #f))

;*---------------------------------------------------------------------*/
;*    doc/documentation ...                                            */
;*---------------------------------------------------------------------*/
(define-service (doc/documentation)
   (let* ((file (service-resource doc/documentation "etc/doc.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    doc/weblet ...                                                   */
;*---------------------------------------------------------------------*/
(define-service (doc/weblet #!key weblet)
   (setup-api-tables!)
   (doc-main (<SCRIPT> :type "text/javascript"
		(select-weblet-pad-on-load weblet))))

;*---------------------------------------------------------------------*/
;*    doc/feature ...                                                  */
;*---------------------------------------------------------------------*/
(define-service (doc/feature #!key feature)
   (setup-api-tables!)
   (doc-main (<SCRIPT> :type "text/javascript"
		(select-api-pad-on-load
		 (service-resource doc/documentation
				   (make-file-name "api" feature))))))

;*---------------------------------------------------------------------*/
;*    doc-main ...                                                     */
;*---------------------------------------------------------------------*/
(define (doc-main script)
   (let* ((req (current-request))
	  (prefs (make-file-name (doc-rc-directory) "doc.prefs")))
      (thread-start! (make-thread (lambda () (setup-api-tables!))))
      (when (file-exists? prefs) (doc-preferences-load prefs))
      (<DOC> :script script
	 ~(define doc/api $doc/api)
	 ~(define doc/tutorial $doc/tutorial)
	 ~(set! api-history-service $doc/api)
	 ~(set! weblet-history-service $doc/api-sans-title)
	 ~(set! tutorial-history-service $doc/tutorial)
	 (<NOTEPAD> :id "doc-notepad"
	    (<NPHEAD> "")
	    (<NPTAB> :id "doc-api"
	       (<NPTABHEAD> "API")
	       (<DIV> :class "doc-tab"
		  (<API>)))
	    
	    (<NPTAB> :id "doc-core"
	       (<NPTABHEAD> "Core")
	       (<DIV> :class "core doc-tab"
		  (doc-page
		   (make-file-name (doc-core-dir) "core.wiki")
		   (make-wiki-request-syntax (doc-api-syntax)))))

	    (<NPTAB> :id "doc-js"
	       (<NPTABHEAD> "JavaScript")
	       (<DIV> :class "js doc-tab"
		  (doc-page
		   (make-file-name (doc-js-dir) "js-interface.wiki")
		   (make-wiki-request-syntax (doc-api-syntax)))))
	    
	    (<NPTAB> :id "doc-libraries"
	       (<NPTABHEAD> "Libraries")
	       (<DIV> :class "doc-tab"
		  (<LIBRARIES>)))
	    
	    (<NPTAB> :id "doc-wiki"
	       (<NPTABHEAD> "Wiki syntax")
	       (<DIV> :class "wiki doc-tab"
		  (doc-page
		   (make-file-name (doc-wiki-dir) "syntax.wiki")
		   (make-wiki-request-syntax (doc-wiki-syntax)))))
	    
	    (<NPTAB> :id "doc-weblets"
	       (<NPTABHEAD> "Weblets")
	       (<DIV> :class "doc-tab"
		  (<WEBLETS> req)))
	    
	    (<NPTAB> :id "doc-tutorials"
	       (<NPTABHEAD> "Tutorials")
	       (<DIV> :class "doc-tab"
		  (<TUTORIALS> req)))
	    
	    (<NPTAB> :id "doc-index"
	       (<NPTABHEAD> "Index")
	       (<DIV> :class "doc-tab"
		  (<INDEX>)))
	    
	    (<NPTAB> :id "doc-search"
	       (<NPTABHEAD> "Search")
	       (<DIV> :class "doc-tab"
		  (<SEARCH>)))))))


