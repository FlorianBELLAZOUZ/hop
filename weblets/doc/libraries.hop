;*=====================================================================*/
;*    serrano/prgm/project/hop/2.0.x/weblets/doc/libraries.hop         */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon May  8 06:21:47 2006                          */
;*    Last change :  Tue Jan 12 09:26:45 2010 (serrano)                */
;*    Copyright   :  2006-10 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    The HOP libraries doc.                                           */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module hopdoc_libraries

   (import hopdoc_config
	   hopdoc_wiki
	   hopdoc_api
	   hopdoc_widgets
	   hopdoc_search)
   
   (export (<LIBRARIES>)
	   (<LIBREF> lbl file)))

;*---------------------------------------------------------------------*/
;*    <LIBRARIES> ...                                                  */
;*---------------------------------------------------------------------*/
(define (<LIBRARIES>)
   (<DIV> :class "libraries"
      (<DIV> :class "libraries-title"
	 :onclick ~(with-hop ($(service () (<LIBRARIES-TOC>)))
		      (lambda (h)
			 (innerHTML-set! "pad-libraries" h)))
	 "HOP Server Libraries")
      (<DIV> :id "pad-libraries" (<LIBRARIES-TOC>))))

;*---------------------------------------------------------------------*/
;*    <LIBRARIES-TOC> ...                                              */
;*---------------------------------------------------------------------*/
(define (<LIBRARIES-TOC>)
   (let ((l (search-libraries (doc-api-dir))))
      (cond
	 ((null? l)
	  "Libraries description")
	 ((null? (cdr l))
	  (<TABLE> :class "api-index"
	     (<COLGROUP> :span 2 :width "50%")
	     (<TR> (<TD> (car l)))))
	 (else
	  (let ((l2 (list-split! l
				 (inexact->exact (ceiling (/ (length l) 2)))
				 "&#160;")))
	     (<TABLE> :class "api-index"
		(<COLGROUP> :span 2 :width "50%")
		(map (lambda (a b) (<TR> (<TD> a) (<TD> b)))
		     (car l2) (cadr l2))))))))

;*---------------------------------------------------------------------*/
;*    <LIBREF> ...                                                     */
;*---------------------------------------------------------------------*/
(define (<LIBREF> lbl file)
   (<SPAN> :class "href" 
      :onclick ~(select-api-pad $(make-file-name (doc-api-dir) file) #f "libraries")
      lbl))

