;; A simple weblet managing an explicit //history//
(<HTML>
   (<HEAD>)
   (<BODY>
      ~(define hist
	  (make-history "h1"
			(lambda (id val)
			   (let ((el (dom-get-element-by-id document id)))
			      (innerHTML-set! el val)))
			"zero"))
      ~(define val 0)
      (let ((box (<SPAN> "_")))
	 (list
	  (<DIV> "Increment the following counter as many times as you
want and then hit the " (<TT> "back button") " of your browser or the
buttons below.")
	  (<BR>)
	  (<BUTTON> :onclick ~(begin
				 (set! val (+ 1 val))
				 (innerHTML-set! $box val)
				 (history-add! hist $box.id val))
	     "+ 1")
	  " "
	  box
	  (<BR>)
	  (<BR>)
	  (<BUTTON> :onclick ~(window.history.back) "back")
	  " "
	  (<BUTTON> :onclick ~(window.history.forward) "forward")))))
