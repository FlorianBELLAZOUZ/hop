;; A live //Wiki//
(define wikiedit-event
   (symbol->string (gensym)))

(define-service (wikiedit/update src)
   (hop-event-broadcast! wikiedit-event (wikiedit-compile src)))

(define *wikiedit-syntax*
   (instantiate::wiki-syntax
      (section1 (lambda expr (<DIV> :class "section1" expr)))
      (section2 (lambda expr (<DIV> :class "section2" expr)))
      (plugins (lambda (id)
		  (case id
		     ((<block>) wikiedit-block)
		     ((<center>) wikiedit-center))))))

(define (wikiedit-block port title expr)
   (apply <DIV> :class "block" (<SPAN> :class "title" title) "\n" expr))

(define (wikiedit-center port title expr)
   (<DIV> :align "center" expr))

(define-service (wikiedit/view)
   (let ((d (<DIV> :style {font-size: small} "")))
      (<HTML>
	 (<HEAD>)
	 (<BODY>
	    ~(add-event-listener! $wikiedit-event "server"
		(lambda (e)
		   (innerHTML-set! $d (event-value e))))
	    d))))

(define (wikiedit-compile src)
   (let ((o (with-handler
	       (lambda (e)
		  (<SPAN> :class "error" (html-string-encode src)))
	       (wiki-string->hop src :syntax *wikiedit-syntax*))))
      (with-output-to-string
	 (lambda ()
	    (xml-write o (current-output-port) (hop-xml-backend))))))


(<HTML>
   (<HEAD>)
   (<BODY>
      (<H3> "Live editor")
      (<TEXTAREA>
	 :class "playground"
	 :rows 8 :cols 80
	 :style "font-size: 100%; color: blue;"
	 :onkeyup ~(with-hop ($wikiedit/update this.value))
	 "__Please__, ++Type in++ //characters//...")
      (<H3> "Live view")
      (<BR>)
      (<IFRAME>
	 :class "view"
	 :style "width: 95%;"
	 :src (wikiedit/view))))
   
