;*=====================================================================*/
;*    serrano/prgm/project/hop/weblets/info/info.hop                   */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon Feb 14 06:14:00 2005                          */
;*    Last change :  Sun May 14 18:13:45 2006 (serrano)                */
;*    Copyright   :  2005-06 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP info weblet                                                  */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module weblet_info)

;*---------------------------------------------------------------------*/
;*    info-event ...                                                   */
;*---------------------------------------------------------------------*/
(define info-event
   (instantiate::hop-event (name "connect")))

;*---------------------------------------------------------------------*/
;*    info-dir ...                                                     */
;*---------------------------------------------------------------------*/
(define info-dir
   (make-file-name (hop-weblets-directory) "info"))

;*---------------------------------------------------------------------*/
;*    traffic-hook ...                                                 */
;*---------------------------------------------------------------------*/
(define (traffic-hook)
   (lambda (req rep)
      (with-access::http-request req (http host port path user date)
	 (signal-hop-event! info-event
			    (<TR> :class "remote"
				  (<TD> :nowrap #t
					host ":" port)
				  (<TD> :nowrap #t
					path)
				  (<TD> :nowrap #t
					(if (user? user) (user-name user) ""))
				  (<TD> :nowrap #t
					date)))
	 rep)))

;*---------------------------------------------------------------------*/
;*    info weblet ...                                                  */
;*---------------------------------------------------------------------*/
(define-service (info)
   (let ((req (the-current-request))
	 (hook (traffic-hook)))
      (hop-http-response-remote-hook-add! hook)
      (if (not (user-authorized-service? (http-request-user req) 'info))
	  (user-access-denied req)
	  (<HTML>
	     (<HEAD> :css "hop-notepad.css" "hop-sorttable.css"
		     :css (format "~a/info.hss" info-dir)
		     :jscript "hop-notepad.js" "hop-sorttable.js")
	     (<BODY>
		:onunload {
		   hop( $(service ()
			   (hop-http-response-remote-hook-remove! hook)
			   #f)(),
			false, false, false );
		}
		(<CENTER>
		   (<TABLE>
		      :class "info"
		      (<TR>
			 (<TD>
			    (<IMG> :src (format "~a/icons/info.png"
						(hop-share-directory))))
			 (<TD>
			    (<TABLE>
			       (<TR>
				  (<TD>
				     (<DIV> :id "title"
					    "Hop Information Center")))
			       (<TR>
				  (<TD>
				     (<DIV> :id "glop"
					    (<NOTEPAD>
					       :inline #f
					       (<NPHEAD> "")
					       (<NPTAB>
						  (<NPTABHEAD> "Misc")
						  (<DELAY>
						     (lambda ()
							(<MISC>))))
					       (<NPTAB>
						  (<NPTABHEAD> "Weblets")
						  (<DELAY>
						     (lambda ()
							(<WEBLETS>))))
					       (<NPTAB>
						  (<NPTABHEAD> "Caches")
						  (<DELAY>
						     (lambda ()
							(<CACHES>))))
					       (<NPTAB>
						  (<NPTABHEAD> "Traffic")
						  (<DELAY>
						     (lambda ()
							(<TRAFFIC>))))))))))))))))))

;*---------------------------------------------------------------------*/
;*    <MISC> ...                                                       */
;*---------------------------------------------------------------------*/
(define (<MISC>)
   (<TABLE>
      :class "hop-info"
      (<TR>
	 (<TH> :align 'right "Version")
	 (<TD> :align 'left (hop-version)))
      (<TR>
	 (<TH> :align 'right "Hop")
	 (<TD> :align 'left (hop-name)))
      (<TR>
	 (<TH> :align 'right "Uptime")
	 (<TD> :align 'left (hop-uptime)))
      (<TR>
	 (<TH> :align 'right "System")
	 (<TD> :align 'left (os-name)))
      (<TR>
	 (<TH> :align 'right "Arch")
	 (<TD> :align 'left (os-arch)))))

;*---------------------------------------------------------------------*/
;*    <WEBLETS> ...                                                    */
;*---------------------------------------------------------------------*/
(define (<WEBLETS>)
   (<TABLE>
      :class "hop-info"
      (map (lambda (rf)
	      (<TR>
		 (<TD>
		    (<A> :href (hop-service-path rf) (hop-service-id rf)))))
	   (sort (hop-weblets)
		 (lambda (rf1 rf2)
		    (string<? (hop-service-path rf1)
			      (hop-service-path rf2)))))))

;*---------------------------------------------------------------------*/
;*    <CACHES> ...                                                     */
;*---------------------------------------------------------------------*/
(define (<CACHES>)
   (define (info-cache c)
      (let* ((l (cache->list c))
	     (d (<DIV>
		   :class "cache-files"
		   (<TABLE>
		      :class "cache-files"
		      (map (lambda (ce)
			      (<TR>
				 (<TD>
				    (<A>
				       :href (format "http://hop/~a"
						     (cache-entry-path ce))
				       (cache-entry-upath ce))
				    " " (round
					 (/ (file-size (cache-entry-path ce))
					    1024))
				    "KB ("
				    (seconds->date (cache-entry-mtime ce))
				    ")")))
			   l)))))
	 (<TABLE>
	    :class "info-cache"
	    (<TR>
	       (<TH> (cache-path c)))
	    (<TR>
	       (<TD> (format "~a files, ~a KB on cache."
			     (length l)
			     (round
			      (/ (apply + (map (lambda (ce)
						  (file-size
						   (cache-entry-path ce)))
					       l))
				 1024)))))
	    (<TR> (<TD> "&nbsp;"))
	    (<TR>
	       (<TD> (<BUTTON>
			:onclick ~(with-hop ($(service ()
						 (cache-clear c)
						 ""))
				     (lambda (h)
					(set! $d.innerHTML h)))
			"clear cache")))
	    (<TR> (<TD> "&nbsp;"))
	    (<TR>
	       (<TD> d)))))
   (let ((d (<DIV>
	       :class "cache-info"
	       "")))
      (<TABLE>
	 :class "hop-caches"
	 (<TR>
	    (<TD>
	       :width "20%"
	       :valign 'top
	       (<DIV>
		  :class "caches"
		  (map (lambda (c)
			  (<DIV>
			     :class "cache-link"
			     :width "100%"
			     :onclick ~(with-hop ($(service () (info-cache c)))
					  (lambda (h)
					     (set! $d.innerHTML h)))
			     (make-file-name
			      (basename (dirname (cache-path c)))
			      (basename (cache-path c)))))
		       (registered-caches))))
	    (<TD>
	       :width "80%"
	       :valign 'top
	       d)))))

;*---------------------------------------------------------------------*/
;*    <TRAFFIC> ...                                                    */
;*---------------------------------------------------------------------*/
(define (<TRAFFIC>)
   (let* ((row (<TR>
		  :class "title"
		  (<TH> "host")
		  (<TH> "path")
		  (<TH> "user")
		  (<TH> "date")))
	  (traffic (<TBODY> row)))
      (<DIV>
       (<DIV> :class "clear"
	      (<BUTTON>
		 :onclick {
		    var e = $traffic
		    var ch = e.childNodes
		    var i

		    for( i = ch.length - 1; i >  0; i-- ) {
		       e.removeChild( ch[ i ] );
	            }
		  } "clear"))
       (<DIV>
	  :class "traffic"
	  (<HOP-EVENT>
	     :handler { hop_append( $traffic )( event ) }
	     :event info-event)
	  (<SORTTABLE> traffic)))))

;*---------------------------------------------------------------------*/
;*    info/about service ...                                           */
;*---------------------------------------------------------------------*/
(define-service (info/about)
  (<WEBLET-ABOUT> :title "info"
		  :subtitle "Displays Hop Informations"
		  :icon (format "~a/icons/info.png" (hop-share-directory))
		  :version (hop-version)
    (<P> [This weblets displays:])
    (<UL>
       (<LI> "General informations on the running hop enegine")
       (<LI> "The list of all the loaded weblets")
       (<LI> "The generated traffic when hop is used as a proxy"))))
