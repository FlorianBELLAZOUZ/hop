;*=====================================================================*/
;*    serrano/prgm/project/hop/weblets/hop/hop.hop                     */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Wed May 10 16:18:48 2006                          */
;*    Last change :  Tue Nov 14 08:56:25 2006 (serrano)                */
;*    Copyright   :  2006 Manuel Serrano                               */
;*    -------------------------------------------------------------    */
;*    HOP initial weblet.                                              */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module hophop
   (import hophop_config))

;*---------------------------------------------------------------------*/
;*    hop ...                                                          */
;*---------------------------------------------------------------------*/
(define-service (hop)
   (let ((t (make-hashtable 20)))
      (hashtable-put! t "hop" #t)
      (hashtable-put! t "home" #t)
      (for-each (lambda (w) (hashtable-put! t w #t)) (hop-builtin-weblets))
      (set! *weblets-table* t))
   (let* ((len 7) (width (format "~a%" (/ 100. len))))
      (<HOP>
	 (<TABLE>
	    :class "directory"
	    (<TR> (<TH> :class "title" "Builtin Weblets"))
	    (<TR> (<TD> :align "left"
		     (<TABLE> :class "weblets"
			(<COLGROUP>
			   (<COL> :span len :width width))
			(hop-builtin-rows len))))
	    (<TR> (<TD> "&nbsp;"))
	    (<TR> (<TH> :class "title" "Additional Weblets"))
	    (<TR> (<TH> :align "left"
		     (<TABLE> :class "weblets"
			(<COLGROUP>
			   (<COL> :span len :width width))
			(hop-additional-rows len))))))))

;*---------------------------------------------------------------------*/
;*    hop/documentation ...                                            */
;*---------------------------------------------------------------------*/
(define-service (hop/documentation)
   (let* ((file (string-append (hop-dir) "/etc/hop.wiki"))
	  (url (format "/hop/doc/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    *weblets-table* ...                                              */
;*---------------------------------------------------------------------*/
(define *weblets-table* #unspecified)

;*---------------------------------------------------------------------*/
;*    hop-builtin-rows ...                                             */
;*---------------------------------------------------------------------*/
(define (hop-builtin-rows len)
   (let ((w (cons (<HOME> (hop-weblets-directory))
		  (map (lambda (x)
			  (<HTD> (hop-weblets-directory) x))
		       (hop-builtin-weblets)))))
      (map <TR> (list-split! w len (<TD> "")))))

;*---------------------------------------------------------------------*/
;*    hop-additional-rows ...                                          */
;*---------------------------------------------------------------------*/
(define (hop-additional-rows len)
   (let* ((w (append-map find-weblets-in-directory (hop-autoload-directories)))
	  (fw (filter-map (lambda (a)
			     (let ((n (cadr (assq 'name a)))
				   (w (cadr (assq 'weblet a))))
				(unless (hashtable-get *weblets-table* n)
				   (hashtable-put! *weblets-table* n #t)
				   (<HTD> (dirname (dirname w)) n))))
			  (sort w (lambda (a b)
				     (let ((namea (cadr (assq 'name a)))
					   (nameb (cadr (assq 'name b))))
					(string<? namea nameb)))))))
      (map <TR> (list-split! fw len (<TD> "")))))

;*---------------------------------------------------------------------*/
;*    <HOME> ...                                                       */
;*---------------------------------------------------------------------*/
(define (<HOME> base)
   (let ((url (hop-url))
	 (logo (format "~a/home/etc/logo.png" base)))
      (<TD>
	 :valign "top"
	 (<TABLE>
	    :width "100%"
	    :onclick ~(set! document.location $url)
	    :title url
	    (<TR>
	       (<TD>
		  :valign "top"
		  :align "center"
		  :class "logo"
		  (<IMG> :class "logo"
			 :src (if (file-exists? logo)
				  logo
				  (string-append (hop-dir)
						 "/etc/default.png")))))
	    (<TR>
	       (<TD>
		  :valign "top"
		  :align "center"
		  :class "legend"
		  (<SPAN> "home")))))))

;*---------------------------------------------------------------------*/
;*    <HTD> ...                                                        */
;*---------------------------------------------------------------------*/
(define (<HTD> base hz)
   (let ((url (format "/hop/~a" hz))
	 (logo (format "~a/~a/etc/logo.png" base hz)))
      (<TD>
	 :valign "top"
	 (<TABLE>
	    :width "100%"
	    :onclick ~(set! document.location $url)
	    :title url
	    (<TR>
	       (<TD>
		  :valign "top"
		  :align "center"
		  :class "logo"
		  (<IMG> :class "logo"
			 :src (if (file-exists? logo)
				  logo
				  (string-append (hop-dir)
						 "/etc/default.png")))))
	    (<TR>
	       (<TD>
		  :valign "top"
		  :align "center"
		  :class "legend"
		  (<SPAN> hz)))))))
	    
;*---------------------------------------------------------------------*/
;*    <HOP> ...                                                        */
;*---------------------------------------------------------------------*/
(define (<HOP> . obj)
   (<HTML>
      (<HEAD> :css (format "~a/hop.hss" (hop-dir)))
      (<BODY>
	 (<DIV>
	    :align "center"
	    (<TABLE>
	       :class "hop"
	       (<TR>
		  (<TD>
		     :valign "top"
		     (<IMG> :class "logo"
			    :src (format "~a/etc/logo.png" (hop-dir))))
		  (<TD>
		     :valign "top"
		     (<TABLE>
			(<TR>
			   (<TD>
			      (<DIV> :id "title" "Hop v" (hop-version))))
			(<TR>
			   (<TD> :align 'left
			      obj)))))))
	 (<FOOT>
	    (<FOOT-BUTTON>
	       :href "http://www.gnu.org/licenses/gpl.html"
	       :title "Gnu GPL"
	       :src "gpl.png")))))
				     
	     
