;*=====================================================================*/
;*    serrano/prgm/project/hop/2.1.x/weblets/weblets/package.hop       */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon Nov 19 05:30:17 2007                          */
;*    Last change :  Fri Mar 26 09:56:50 2010 (serrano)                */
;*    Copyright   :  2007-10 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    Functions for dealing with packages.                             */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module weblets_package

   (import weblets_config)
   
   (export (package-filename? ::bstring)
	   (package-name-parse ::bstring)
	   (package-url-parse ::bstring)
	   (package-info ::bstring)))

;*---------------------------------------------------------------------*/
;*    package-filename? ...                                            */
;*---------------------------------------------------------------------*/
(define (package-filename? name)
   (string-suffix-ci? ".hz" name))

;*---------------------------------------------------------------------*/
;*    package-sans-suffix ...                                          */
;*---------------------------------------------------------------------*/
(define (package-sans-suffix name)
   (substring name 0 (-fx (string-length name) 3)))

;*---------------------------------------------------------------------*/
;*    package-name-parse ...                                           */
;*    -------------------------------------------------------------    */
;*    Parses a package file name and returns the base name, the        */
;*    version number, and release number.                              */
;*    The syntax of a package name is:                                 */
;*       .*-[0-9]+.0-9]+.[0-9]+(-{pre,rc}?[0-9]+)?.hz                  */
;*---------------------------------------------------------------------*/
(define (package-name-parse name)
   (unless (package-filename? name)
      (error 'package-name-parse "Illegal package name, wrong suffix" name))
   (let* ((n (package-sans-suffix (basename name)))
          (index (string-index-right n #\-))
          (vdot (string-index-right n #\.)))
      (cond
         ((not index)
          (error 'package-name-parse "Illegal package name" name))
         ((and (fixnum? vdot) (>fx vdot index))
          ;; a package without release
          (let* ((version (substring n (+fx 1 index) (string-length n)))
                 (base (substring n 0 index)))
	     (values base version)))
         (else
          ;; a package with release
          (let ((vindex (string-index-right n #\- (-fx index 1))))
             (if (not vindex)
                 (error 'package-name-parse "Illegal package name" name)
                 (let* ((version (substring n (+fx 1 vindex) (string-length n)))
                        (base (substring n 0 vindex)))
		    (values base version))))))))

;*---------------------------------------------------------------------*/
;*    package-url-parse ...                                            */
;*---------------------------------------------------------------------*/
(define (package-url-parse url)
   (multiple-value-bind (scheme userinfo host port path)
      (url-parse url)
      (package-name-parse (basename path))))

;*---------------------------------------------------------------------*/
;*    package-info ...                                                 */
;*---------------------------------------------------------------------*/
(define (package-info url)
   (let* ((ip (open-input-gzip-file url))
	  (info (make-file-name "etc" "weblet.info"))
	  (len (string-length info)))
      (unwind-protect
	 (let loop ()
	    (let ((h (tar-read-header ip)))
	       (when (tar-header? h)
		  (case (tar-header-type h)
		     ((dir)
		      (loop))
		     ((normal)
		      (let ((b (tar-read-block h ip))
			    (n (tar-header-name h)))
			 (if (substring-at? n info (-fx (string-length n) len))
			     (charset-convert b (hop-locale) (hop-charset))
			     (loop))))
		     (else
		      #f)))))
	 (close-input-port ip))))





