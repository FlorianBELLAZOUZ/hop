;*=====================================================================*/
;*    serrano/prgm/project/hop/2.2.x/weblets/test/test.hop             */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Mon Feb 14 06:14:00 2005                          */
;*    Last change :  Wed Nov 16 15:58:53 2011 (serrano)                */
;*    Copyright   :  2005-11 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    HOP client side testing                                          */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module weblet_test
   (import weblet_test-dom
	   weblet_test-image
	   weblet_test-withhop
	   weblet_test-event
	   weblet_test-hss
	   weblet_test-fl
	   weblet_test-tree
	   weblet_test-tabslider
	   weblet_test-spage
	   weblet_test-notepad
	   weblet_test-paned
	   weblet_test-busy
	   weblet_test-sorttable
	   weblet_test-slider
	   weblet_test-canvas
	   weblet_test-window
	   weblet_test-fselector
	   weblet_test-audio
	   weblet_test-xhtml
	   weblet_test-svg
	   weblet_test-mathml
	   weblet_test-spinbutton
	   weblet_test-colorchooser
	   weblet_test-widget
	   weblet_test-gauge)
   (export (<TEST-HTML> . args)))

;*---------------------------------------------------------------------*/
;*    hoprc-dir ...                                                    */
;*---------------------------------------------------------------------*/
(define-parameter hoprc-dir
   (make-file-path (getenv "HOME") ".config" "hop"))
   
;*---------------------------------------------------------------------*/
;*    test/documentation ...                                           */
;*---------------------------------------------------------------------*/
(define-service (test/documentation)
   (let* ((file (service-resource test/documentation "etc/test.wiki"))
	  (url (format "/hop/test/weblet?weblet=~a" file)))
      (instantiate::http-response-string
	 (start-line "HTTP/1.0 301 Moved Permanently")
	 (header (list (cons 'location: url))))))

;*---------------------------------------------------------------------*/
;*    test ...                                                         */
;*---------------------------------------------------------------------*/
(define-service (test)
   (<TEST-HTML>
      (<DIV> :class "maintest"
	 (<H4> "Simple tests")
	 (<UL>
	    (<LI> (<A> :href test/dom :target "_blank" "Dom"))
	    (<LI> (<A> :href test/images :target "_blank" "Images"))
	    (<LI> (<A> :href test/withhop :target "_blank" "with-hop"))
	    (<LI> (<A> :href test/events :target "_blank" "Events"))
	    (<LI> (<A> :href test/hss :target "_blank" "HSS")))
	 (<H4> "Hop Widget tests")
	 (<UL>
	    (<LI> (<A> :href test/fl :target "_blank" "Fold lists"))
	    (<LI> (<A> :href test/notepad :target "_blank" "Notepads"))
	    (<LI> (<A> :href test/paned :target "_blank" "Pans"))
	    (<LI> (<A> :href test/slider :target "_blank" "Sliders"))
	    (<LI> (<A> :href test/sorttable :target "_blank" "Sorttables"))
	    (<LI> (<A> :href test/tree :target "_blank" "Trees"))
	    (<LI> (<A> :href test/tabslider :target "_blank" "Tabsliders"))
	    (<LI> (<A> :href test/spage :target "_blank" "Spages"))
	    (<LI> (<A> :href test/canvas :target "_blank" "Canvas"))
	    (<LI> (<A> :href test/window :target "_blank" "Windows"))
	    (<LI> (<A> :href test/fselector :target "_blank" "File Selector"))
	    (<LI> (<A> :href test/hss :target "_blank" "Label Frame"))
	    (<LI> (<A> :href test/spinbutton :target "_blank" "SpinButton"))
	    (<LI> (<A> :href test/colorchooser :target "_blank" "ColorChooser"))
	    (<LI> (<A> :href test/gauge :target "_blank" "Gauge"))
	    (<LI> (<A> :href test/widget :target "_blank" "Custom Widget")))
	 (<H4> "XML and HTML")
	 (<UL>
	    (<LI> (<A> :href test/xhtml :target "_blank" "XHTML"))
	    (<LI> (<A> :href test/svg :target "_blank" "SVG"))
	    (<LI> (<A> :href test/mathml :target "_blank" "MathML")))
	 (<H4> "Miscellaneous tests")
	 (<UL>
	    (<LI> (<A> :href test/busy :target "_blank" "Busy anim"))
	    (<LI> (<A> :href test/audio :target "_blank" "Audio"))))))

;*---------------------------------------------------------------------*/
;*    <TEST-HTML> ...                                                  */
;*---------------------------------------------------------------------*/
(define-xml-compound <TEST-HTML> ((include #f)
				  (onload #f)
				  (source #f)
				  (backend #f)
				  body)
   (if (isa? backend xml-backend)
       (with-access::xml-backend backend (mime-type)
	  (instantiate::http-response-hop
	     (backend backend)
	     (content-type mime-type)
	     (xml (test-html include onload source body))))
       (test-html include onload source body)))

;*---------------------------------------------------------------------*/
;*    test-html ...                                                    */
;*---------------------------------------------------------------------*/
(define (test-html include onload source body)
   (let ((src (when source
		 (<PRE> :style "display: none"
		    (with-input-from-file source
		       (lambda ()
			  (html-string-encode (read-string))))))))
      (<HTML>
	 (cond
	    ((string? include)
	     (<HEAD> :title "Hop client-side testing"
		:favicon (service-resource test "etc/favicon.png")
		:include "hop-foot"
		:css (service-resource test "test.hss") :include include))
	    ((pair? include)
	     (apply <HEAD> :title "Hop client-side testing"
		    :favicon (service-resource test "etc/favicon.png")
		    :include "hop-foot"
		    :css (service-resource test "test.hss") :include include))
	    (else
	     (<HEAD> :title "Hop client-side testing"
		:favicon (service-resource test "etc/favicon.png")
		:include "hop-foot"
		:css (service-resource test "test.hss"))))
	 (<BODY> :onload onload
	    
	    ~(define (toggle-source)
		(if (string=? (node-style-get $src "display") "none")
		    (node-style-set! $src "display" "block")
		    (node-style-set! $src "display" "none")))

	    (<DIV> :align "center"
	       (<TABLE> :class "test"
		  (<COLGROUP> (<COL> :width "0*"))
		  (<TR>
		     (<TD> :id "logo"
			(<IMG> :id "logo-img"
			   :src (service-resource test "etc/logo.png")))
		     (<TD> (<TABLE> :width "100%" :class "content"
			      (<TR>
				 (<TD> :id "title" "Hop client-side testing"))
			      (when source
				 (<TR> 
				    (<TD> :id "source"
				       "View source: "
				       (<SPAN> :onclick ~(toggle-source)
					  (basename source))
				       src)))
			      (<TR>
				 (<TD> 
				    body)))))))
	    (<FOOT>)))))
   
