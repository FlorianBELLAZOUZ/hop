;*=====================================================================*/
;*    serrano/prgm/project/hop/2.2.x/widget/spage.hop                  */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Fri Aug 14 08:24:36 2009                          */
;*    Last change :  Thu Mar 31 10:08:28 2011 (serrano)                */
;*    Copyright   :  2009-11 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    SlidePage server-side implementation                             */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module __hopwidget-spage

   (library hop)

   (static (class xml-sphead-element::xml-element)
	   (class xml-sptabhead-element::xml-element))

   (export (<SPAGE> . obj)
	   (<SPHEAD> . obj)
	   (<SPTAB> . obj)
	   (<SPTABHEAD> . obj)))

;*---------------------------------------------------------------------*/
;*    SPAGE ...                                                        */
;*---------------------------------------------------------------------*/
(define-xml-compound <SPAGE> ((id (xml-make-id 'SPAGE) string)
			      (onchange #f)
			      (class #f)
			      (attr)
			      body)
   (let ((head (filter xml-sphead-element? body))
	 (body (filter (lambda (x) (not (xml-sphead-element? x))) body)))
      (instantiate::xml-element
	 (tag 'div)
	 (id id)
	 (attributes `(:hssclass "hop-spage" :class ,class ,@attr))
	 (body (list (if (null? head)
			 (<SPHEAD> :style "display: none" "?")
			 (car head))
		     (<SPAN> :hssclass "hop-spstyle" " ")
		     (<DIV> :hssclass "hop-spwindow"
			(<DIV> :hssclass "hop-spviewport"
			   (<DIV> :hssclass "hop-spbody"
			      :class class
			      body)))
		     ~(add-event-listener! $id "ready"
			 (lambda (e)
			    (spage-init $id
					$(when (xml-tilde? onchange)
					    (sexp->xml-tilde
					     `(lambda (event)
						 ,(xml-tilde->sexp onchange))
					     (xml-tilde-env onchange)
					     (xml-tilde-menv onchange)))))))))))

;*---------------------------------------------------------------------*/
;*    SPHEAD ...                                                       */
;*---------------------------------------------------------------------*/
(define-xml-compound <SPHEAD> ((id (xml-make-id 'SPHEAD) string)
			       (attr)
			       body)
   (instantiate::xml-sphead-element
      (tag 'div)
      (attributes `(:hssclass "hop-sphead" ,@attr))
      (id id)
      (body (list (<DIV> body)
		  (<DIV> :hssclass "hop-sppopbutton"
		     :onclick ~(spage-pop-update this)
		     (<SPAN> ""))))))

;*---------------------------------------------------------------------*/
;*    svc-stamp ...                                                    */
;*---------------------------------------------------------------------*/
(define svc-stamp 0)

;*---------------------------------------------------------------------*/
;*    get-svc-stamp ...                                                */
;*---------------------------------------------------------------------*/
(define (get-svc-stamp)
   (let ((s svc-stamp))
      (set! svc-stamp (+fx 1 svc-stamp))
      s))

;*---------------------------------------------------------------------*/
;*    SPTAB ...                                                        */
;*---------------------------------------------------------------------*/
(define-xml-compound <SPTAB> ((id (xml-make-id 'SPTAB) string)
			      (onselect #f)
			      (class #f)
			      (attr)
			      body)
   
   (define (init-sptab id onselect)
      (if (xml-tilde? onselect)
	  (sexp->xml-tilde
	   `(add-event-listener! ,id "ready"
	       (lambda (event)
		  (let ((el this))
		     (set! el.hop_add_event_listener sptab-add-event-listener!)
		     (set! el.onselect
			   (lambda (event)
			      ,(xml-tilde->sexp onselect)))
		     #unspecified)))
	   (xml-tilde-env onselect)
	   (xml-tilde-menv onselect))
	  (sexp->xml-tilde
	   `(add-event-listener! ,id "ready"
	       (lambda (event)
		  (set! this.hop_add_event_listener sptab-add-event-listener!)
		  #unspecified)))))
   
   (let* ((head (filter xml-sphead-element? body))
	  (body (filter (lambda (x) (not (xml-sphead-element? x))) body)))
      (if (and (pair? body) (null? (cdr body)) (xml-delay? (car body)))
	  ;; a dynamic tab
	  (let ((svc (service :name (format "sptab/~a/~a" id (get-svc-stamp)) ()
			(<DIV> :hssclass "hop-spbody"
			   :class class
			   attr
			   body))))
	     (<DIV> :hssclass "hop-sptab" :class class  :id id
		:onclick ~(spage-push-service this $svc)
		(if (null? head) "?" (car head))
		(init-sptab id onselect)))
	  ;; a static tab
	  (<DIV> :hssclass "hop-sptab" :class class :id id
	     :onclick ~(spage-push-node this (dom-first-child (dom-last-child this)))
	     (if (null? head) "?" (car head))
	     (init-sptab id onselect)
	     (<DIV> :hssclass "hop-sptab-static"
		(<DIV> :hssclass "hop-spbody":class class attr body))))))

;*---------------------------------------------------------------------*/
;*    SPTABHEAD ...                                                    */
;*---------------------------------------------------------------------*/
(define-xml-compound <SPTABHEAD> ((id (xml-make-id 'SPTABHEAD) string)
				  (attr)
				  body)
   (instantiate::xml-sphead-element
      (tag 'div)
      (attributes `(:hssclass "hop-sptabhead" ,@attr))
      (id id)
      (body (list (<SPAN> body)
		  (<DIV> :hssclass "hop-sppushbutton" "")))))
