;*=====================================================================*/
;*    serrano/prgm/project/hop/2.1.x/widget/spinbutton.hop             */
;*    -------------------------------------------------------------    */
;*    Author      :  Manuel Serrano                                    */
;*    Creation    :  Thu Jun 11 18:49:09 2009                          */
;*    Last change :  Tue Feb 16 11:58:00 2010 (serrano)                */
;*    Copyright   :  2009-10 Manuel Serrano                            */
;*    -------------------------------------------------------------    */
;*    Spin Buttons server side implementation                          */
;*=====================================================================*/

;*---------------------------------------------------------------------*/
;*    The module                                                       */
;*---------------------------------------------------------------------*/
(module __hopwidget-spinbutton

   (library hop)
   
   (export  (<SPINBUTTON> . ::obj)))

;*---------------------------------------------------------------------*/
;*    <SPINBUTTON> ...                                                 */
;*---------------------------------------------------------------------*/
(define-xml-compound <SPINBUTTON> ((id #unspecified string)
				   (min 0)
				   (max 100)
				   (value 0)
				   (onchange #f)
				   (attrs)
				   body)
   
   (define (input-width)
      (let loop ((v ((@ maxfx __r4_numbers_6_5_fixnum) (abs min) (abs max)))
		 (w 0))
	 (if (>fx v 0)
	     (loop (/fx v 10) (+fx w 1))
	     (if (or (<fx min 0) (<fx max 0))
		 (+fx 1 w)
		 w))))

   (if (pair? body)
       (error '<SPINBUTTON> "arguments ignored" body)
       (let* ((id (xml-make-id id 'SPINBUTTON))
	      (w (input-width))
	      (body (list
		     (<TR>
			(<TD> :class "hop-spinbutton-value" :rowspan 2
			   (<INPUT> :class "hop-spinbutton-entry"
			      :id (string-append id "-entry")
			      :type 'text
			      :style (format "width: ~aem" w)
			      :onchange ~(spinbutton-value-update! $id (string->number this.value))
			      :value value)
			   (<INPUT> :class "hop-spinbutton-entry-onchange"
			      :id (string-append id "-onchange")
			      :onchange onchange))
			(<TD> :class "hop-spinbutton-buttons"
			   (<TABLE>
			      (<TR>
				 (<TD> :class "hop-spinbutton-button-top"
				    :onmousedown ~(spinbutton-inc! $id)
				    (<DIV> "&#9650;")))
			      (<TR>
				 (<TD> :class "hop-spinbutton-button-bottom"
				    :onmousedown ~(spinbutton-dec! $id)
				    (<DIV> "&#9660;")))))))))
	  (<TABLE> :id id :hssclass "hop-spinbutton"
	     :cellspacing 0
	     :cellpadding 0
	     :value (if (xml-tilde? value) value ~$value)
	     :min (if (xml-tilde? min) min ~$min)
	     :max (if (xml-tilde? max) max ~$max)
	     body))))
